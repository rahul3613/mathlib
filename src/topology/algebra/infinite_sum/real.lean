/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import algebra.big_operators.intervals
import topology.algebra.infinite_sum.order
import topology.instances.real

/-!
# Infinite sum in the reals

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file provides lemmas about Cauchy sequences in terms of infinite sums.
-/

open filter finset
open_locale big_operators nnreal topology

variables {Œ± : Type*}

/-- If the extended distance between consecutive points of a sequence is estimated
by a summable series of `nnreal`s, then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_edist_le_of_summable [pseudo_emetric_space Œ±] {f : ‚Ñï ‚Üí Œ±} (d : ‚Ñï ‚Üí ‚Ñù‚â•0)
  (hf : ‚àÄ n, edist (f n) (f n.succ) ‚â§ d n) (hd : summable d) : cauchy_seq f :=
begin
  refine emetric.cauchy_seq_iff_nnreal.2 (Œª Œµ Œµpos, _),
  -- Actually we need partial sums of `d` to be a Cauchy sequence
  replace hd : cauchy_seq (Œª (n : ‚Ñï), ‚àë x in range n, d x) :=
    let ‚ü®_, H‚ü© := hd in H.tendsto_sum_nat.cauchy_seq,
  -- Now we take the same `N` as in one of the definitions of a Cauchy sequence
  refine (metric.cauchy_seq_iff'.1 hd Œµ (nnreal.coe_pos.2 Œµpos)).imp (Œª N hN n hn, _),
  have hsum := hN n hn,
  -- We simplify the known inequality
  rw [dist_nndist, nnreal.nndist_eq, ‚Üê sum_range_add_sum_Ico _ hn, add_tsub_cancel_left] at hsum,
  norm_cast at hsum,
  replace hsum := lt_of_le_of_lt (le_max_left _ _) hsum,
  rw edist_comm,
  -- Then use `hf` to simplify the goal to the same form
  apply lt_of_le_of_lt (edist_le_Ico_sum_of_edist_le hn (Œª k _ _, hf k)),
  assumption_mod_cast
end

variables [pseudo_metric_space Œ±] {f : ‚Ñï ‚Üí Œ±} {a : Œ±}

/-- If the distance between consecutive points of a sequence is estimated by a summable series,
then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_dist_le_of_summable (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
  (hd : summable d) : cauchy_seq f :=
begin
  refine metric.cauchy_seq_iff'.2 (ŒªŒµ Œµpos, _),
  replace hd : cauchy_seq (Œª (n : ‚Ñï), ‚àë x in range n, d x) :=
    let ‚ü®_, H‚ü© := hd in H.tendsto_sum_nat.cauchy_seq,
  refine (metric.cauchy_seq_iff'.1 hd Œµ Œµpos).imp (Œª N hN n hn, _),
  have hsum := hN n hn,
  rw [real.dist_eq, ‚Üê sum_Ico_eq_sub _ hn] at hsum,
  calc dist (f n) (f N) = dist (f N) (f n) : dist_comm _ _
  ... ‚â§ ‚àë x in Ico N n, d x : dist_le_Ico_sum_of_dist_le hn (Œª k _ _, hf k)
  ... ‚â§ |‚àë x in Ico N n, d x| : le_abs_self _
  ... < Œµ : hsum
end

lemma cauchy_seq_of_summable_dist (h : summable (Œª n, dist (f n) (f n.succ))) : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ (Œª _, le_rfl) h

lemma dist_le_tsum_of_dist_le_of_tendsto (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
  (hd : summable d) {a : Œ±} (ha : tendsto f at_top (ùìù a)) (n : ‚Ñï) :
  dist (f n) a ‚â§ ‚àë' m, d (n + m) :=
begin
  refine le_of_tendsto (tendsto_const_nhds.dist ha)
    (eventually_at_top.2 ‚ü®n, Œª m hnm, _‚ü©),
  refine le_trans (dist_le_Ico_sum_of_dist_le hnm (Œª k _ _, hf k)) _,
  rw [sum_Ico_eq_sum_range],
  refine sum_le_tsum (range _) (Œª _ _, le_trans dist_nonneg (hf _)) _,
  exact hd.comp_injective (add_right_injective n)
end

lemma dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
  (hd : summable d) (ha : tendsto f at_top (ùìù a)) :
  dist (f 0) a ‚â§ tsum d :=
by simpa only [zero_add] using dist_le_tsum_of_dist_le_of_tendsto d hf hd ha 0

lemma dist_le_tsum_dist_of_tendsto (h : summable (Œª n, dist (f n) (f n.succ)))
  (ha : tendsto f at_top (ùìù a)) (n) :
  dist (f n) a ‚â§ ‚àë' m, dist (f (n + m)) (f (n + m).succ) :=
show dist (f n) a ‚â§ ‚àë' m, (Œªx, dist (f x) (f x.succ)) (n + m), from
dist_le_tsum_of_dist_le_of_tendsto (Œª n, dist (f n) (f n.succ)) (Œª _, le_rfl) h ha n

lemma dist_le_tsum_dist_of_tendsto‚ÇÄ (h : summable (Œª n, dist (f n) (f n.succ)))
  (ha : tendsto f at_top (ùìù a)) :
  dist (f 0) a ‚â§ ‚àë' n, dist (f n) (f n.succ) :=
by simpa only [zero_add] using dist_le_tsum_dist_of_tendsto h ha 0
