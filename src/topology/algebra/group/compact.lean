/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Patrick Massot
-/
import topology.algebra.group.basic
import topology.compact_open
import topology.sets.compacts

/-!
# Additional results on topological groups

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Two results on topological groups that have been separated out as they require more substantial
imports developing either positive compacts or the compact open topology.

-/

open classical set filter topological_space function
open_locale classical topology filter pointwise

universes u v w x
variables {Œ± : Type u} {Œ≤ : Type v} {G : Type w} {H : Type x}

section

/-! Some results about an open set containing the product of two sets in a topological group. -/

variables [topological_space G] [group G] [topological_group G]

/-- Every separated topological group in which there exists a compact set with nonempty interior
is locally compact. -/
@[to_additive "Every separated topological group in which there exists a compact set with nonempty
interior is locally compact."]
lemma topological_space.positive_compacts.locally_compact_space_of_group
  [t2_space G] (K : positive_compacts G) :
  locally_compact_space G :=
begin
  refine locally_compact_of_compact_nhds (Œª x, _),
  obtain ‚ü®y, hy‚ü© := K.interior_nonempty,
  let F := homeomorph.mul_left (x * y‚Åª¬π),
  refine ‚ü®F '' K, _, K.is_compact.image F.continuous‚ü©,
  suffices : F.symm ‚Åª¬π' K ‚àà ùìù x, by { convert this, apply equiv.image_eq_preimage },
  apply continuous_at.preimage_mem_nhds F.symm.continuous.continuous_at,
  have : F.symm x = y, by simp [F, homeomorph.mul_left_symm],
  rw this,
  exact mem_interior_iff_mem_nhds.1 hy
end

end

section quotient
variables [group G] [topological_space G] [topological_group G] {Œì : subgroup G}

@[to_additive]
instance quotient_group.has_continuous_smul [locally_compact_space G] :
  has_continuous_smul G (G ‚ß∏ Œì) :=
{ continuous_smul := begin
    let F : G √ó G ‚ß∏ Œì ‚Üí G ‚ß∏ Œì := Œª p, p.1 ‚Ä¢ p.2,
    change continuous F,
    have H : continuous (F ‚àò (Œª p : G √ó G, (p.1, quotient_group.mk p.2))),
    { change continuous (Œª p : G √ó G, quotient_group.mk (p.1 * p.2)),
      refine continuous_coinduced_rng.comp continuous_mul },
    exact quotient_map.continuous_lift_prod_right quotient_map_quotient_mk H,
  end }

end quotient
