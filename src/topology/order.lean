/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import topology.tactic

/-!
# Ordering on topologies and (co)induced topologies

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Topologies on a fixed type `Œ±` are ordered, by reverse inclusion.
That is, for topologies `t‚ÇÅ` and `t‚ÇÇ` on `Œ±`, we write `t‚ÇÅ ‚â§ t‚ÇÇ`
if every set open in `t‚ÇÇ` is also open in `t‚ÇÅ`.
(One also calls `t‚ÇÅ` finer than `t‚ÇÇ`, and `t‚ÇÇ` coarser than `t‚ÇÅ`.)

Any function `f : Œ± ‚Üí Œ≤` induces
       `induced f : topological_space Œ≤ ‚Üí topological_space Œ±`
and  `coinduced f : topological_space Œ± ‚Üí topological_space Œ≤`.
Continuity, the ordering on topologies and (co)induced topologies are
related as follows:
* The identity map (Œ±, t‚ÇÅ) ‚Üí (Œ±, t‚ÇÇ) is continuous iff t‚ÇÅ ‚â§ t‚ÇÇ.
* A map f : (Œ±, t) ‚Üí (Œ≤, u) is continuous
    iff             t ‚â§ induced f u   (`continuous_iff_le_induced`)
    iff coinduced f t ‚â§ u             (`continuous_iff_coinduced_le`).

Topologies on Œ± form a complete lattice, with ‚ä• the discrete topology
and ‚ä§ the indiscrete topology.

For a function f : Œ± ‚Üí Œ≤, (coinduced f, induced f) is a Galois connection
between topologies on Œ± and topologies on Œ≤.

## Implementation notes

There is a Galois insertion between topologies on Œ± (with the inclusion ordering)
and all collections of sets in Œ±. The complete lattice structure on topologies
on Œ± is defined as the reverse of the one obtained via this Galois insertion.

## Tags

finer, coarser, induced topology, coinduced topology

-/

open function set filter
open_locale topology filter

universes u v w

namespace topological_space
variables {Œ± : Type u}

/-- The open sets of the least topology containing a collection of basic sets. -/
inductive generate_open (g : set (set Œ±)) : set Œ± ‚Üí Prop
| basic  : ‚àÄs‚ààg, generate_open s
| univ   : generate_open univ
| inter  : ‚àÄs t, generate_open s ‚Üí generate_open t ‚Üí generate_open (s ‚à© t)
| sUnion : ‚àÄk, (‚àÄs‚ààk, generate_open s) ‚Üí generate_open (‚ãÉ‚ÇÄ k)

/-- The smallest topological space containing the collection `g` of basic sets -/
def generate_from (g : set (set Œ±)) : topological_space Œ± :=
{ is_open        := generate_open g,
  is_open_univ   := generate_open.univ,
  is_open_inter  := generate_open.inter,
  is_open_sUnion := generate_open.sUnion }

lemma is_open_generate_from_of_mem {g : set (set Œ±)} {s : set Œ±} (hs : s ‚àà g) :
  is_open[generate_from g] s :=
generate_open.basic s hs

lemma nhds_generate_from {g : set (set Œ±)} {a : Œ±} :
  @nhds Œ± (generate_from g) a = (‚®Ös‚àà{s | a ‚àà s ‚àß s ‚àà g}, ùìü s) :=
begin
  rw nhds_def,
  refine le_antisymm (binfi_mono $ Œª s ‚ü®as, sg‚ü©, ‚ü®as, generate_open.basic _ sg‚ü©) _,
  refine le_infi‚ÇÇ (Œª s hs, _), cases hs with ha hs,
  induction hs,
  case basic : s hs { exact infi‚ÇÇ_le _ ‚ü®ha, hs‚ü© },
  case univ : { exact le_top.trans_eq principal_univ.symm },
  case inter : s t hs' ht' hs ht { exact (le_inf (hs ha.1) (ht ha.2)).trans_eq inf_principal },
  case sUnion : S hS' hS
  { rcases ha with  ‚ü®t, htS, hat‚ü©,
    exact (hS t htS hat).trans (principal_mono.2 $ subset_sUnion_of_mem htS) }
end

lemma tendsto_nhds_generate_from {Œ≤ : Type*} {m : Œ± ‚Üí Œ≤} {f : filter Œ±} {g : set (set Œ≤)} {b : Œ≤}
  (h : ‚àÄs‚ààg, b ‚àà s ‚Üí m ‚Åª¬π' s ‚àà f) : tendsto m f (@nhds Œ≤ (generate_from g) b) :=
by rw [nhds_generate_from]; exact
  (tendsto_infi.2 $ assume s, tendsto_infi.2 $ assume ‚ü®hbs, hsg‚ü©, tendsto_principal.2 $ h s hsg hbs)

/-- Construct a topology on Œ± given the filter of neighborhoods of each point of Œ±. -/
protected def mk_of_nhds (n : Œ± ‚Üí filter Œ±) : topological_space Œ± :=
{ is_open        := Œªs, ‚àÄa‚ààs, s ‚àà n a,
  is_open_univ   := assume x h, univ_mem,
  is_open_inter  := assume s t hs ht x ‚ü®hxs, hxt‚ü©, inter_mem (hs x hxs) (ht x hxt),
  is_open_sUnion := assume s hs a ‚ü®x, hx, hxa‚ü©,
    mem_of_superset (hs x hx _ hxa) (set.subset_sUnion_of_mem hx) }

lemma nhds_mk_of_nhds (n : Œ± ‚Üí filter Œ±) (a : Œ±)
  (h‚ÇÄ : pure ‚â§ n) (h‚ÇÅ : ‚àÄ a s, s ‚àà n a ‚Üí ‚àÉ t ‚àà n a, t ‚äÜ s ‚àß ‚àÄa' ‚àà t, s ‚àà n a') :
  @nhds Œ± (topological_space.mk_of_nhds n) a = n a :=
begin
  letI := topological_space.mk_of_nhds n,
  refine le_antisymm (assume s hs, _) (assume s hs, _),
  { have h‚ÇÄ : {b | s ‚àà n b} ‚äÜ s := assume b hb, mem_pure.1 $ h‚ÇÄ b hb,
    have h‚ÇÅ : {b | s ‚àà n b} ‚àà ùìù a,
    { refine is_open.mem_nhds (assume b (hb : s ‚àà n b), _) hs,
      rcases h‚ÇÅ _ _ hb with ‚ü®t, ht, hts, h‚ü©,
      exact mem_of_superset ht h },
    exact mem_of_superset h‚ÇÅ h‚ÇÄ },
  { rcases (@mem_nhds_iff Œ± (topological_space.mk_of_nhds n) _ _).1 hs with ‚ü®t, hts, ht, hat‚ü©,
    exact (n a).sets_of_superset (ht _ hat) hts },
end

lemma nhds_mk_of_nhds_single [decidable_eq Œ±] {a‚ÇÄ : Œ±} {l : filter Œ±} (h : pure a‚ÇÄ ‚â§ l) (b : Œ±) :
  @nhds Œ± (topological_space.mk_of_nhds $ update pure a‚ÇÄ l) b =
    (update pure a‚ÇÄ l : Œ± ‚Üí filter Œ±) b :=
begin
  refine nhds_mk_of_nhds _ _ (le_update_iff.mpr ‚ü®h, Œª _ _, le_rfl‚ü©) (Œª a s hs, _),
  rcases eq_or_ne a a‚ÇÄ with rfl|ha,
  { refine ‚ü®s, hs, subset.rfl, Œª b hb, _‚ü©,
    rcases eq_or_ne b a with rfl|hb,
    { exact hs },
    { rwa [update_noteq hb] } },
  { have hs' := hs,
    rw [update_noteq ha] at hs ‚ä¢,
    exact ‚ü®{a}, rfl, singleton_subset_iff.mpr hs, forall_eq.2 hs'‚ü© }
end

lemma nhds_mk_of_nhds_filter_basis (B : Œ± ‚Üí filter_basis Œ±) (a : Œ±) (h‚ÇÄ : ‚àÄ x (n ‚àà B x), x ‚àà n)
  (h‚ÇÅ : ‚àÄ x (n ‚àà B x), ‚àÉ n‚ÇÅ ‚àà B x, n‚ÇÅ ‚äÜ n ‚àß ‚àÄ x' ‚àà n‚ÇÅ, ‚àÉ n‚ÇÇ ‚àà B x', n‚ÇÇ ‚äÜ n) :
  @nhds Œ± (topological_space.mk_of_nhds (Œª x, (B x).filter)) a = (B a).filter :=
begin
  rw topological_space.nhds_mk_of_nhds;
  intros x n hn;
  obtain ‚ü®m, hm‚ÇÅ, hm‚ÇÇ‚ü© := (B x).mem_filter_iff.mp hn,
  { exact hm‚ÇÇ (h‚ÇÄ _ _ hm‚ÇÅ), },
  { obtain ‚ü®n‚ÇÅ, hn‚ÇÅ, hn‚ÇÇ, hn‚ÇÉ‚ü© := h‚ÇÅ x m hm‚ÇÅ,
    refine ‚ü®n‚ÇÅ, (B x).mem_filter_of_mem hn‚ÇÅ, hn‚ÇÇ.trans hm‚ÇÇ, Œª x' hx', (B x').mem_filter_iff.mp _‚ü©,
    obtain ‚ü®n‚ÇÇ, hn‚ÇÑ, hn‚ÇÖ‚ü© := hn‚ÇÉ x' hx',
    exact ‚ü®n‚ÇÇ, hn‚ÇÑ, hn‚ÇÖ.trans hm‚ÇÇ‚ü©, },
end

section lattice

/-- The ordering on topologies on the type `Œ±`. `t ‚â§ s` if every set open in `s` is also open in `t`
(`t` is finer than `s`). -/
instance : partial_order (topological_space Œ±) :=
{ le := Œª s t, ‚àÄ U, is_open[t] U ‚Üí is_open[s] U,
  .. partial_order.lift (Œª s, order_dual.to_dual (is_open[s])) (Œª _ _, topological_space_eq) }

protected lemma le_def {Œ±} {t s : topological_space Œ±} : t ‚â§ s ‚Üî is_open[s] ‚â§ is_open[t] :=
iff.rfl

lemma le_generate_from_iff_subset_is_open {g : set (set Œ±)} {t : topological_space Œ±} :
  t ‚â§ topological_space.generate_from g ‚Üî g ‚äÜ {s | is_open[t] s} :=
‚ü®Œª ht s hs, ht _ $ generate_open.basic s hs, Œª hg s hs, hs.rec_on (assume v hv, hg hv)
  t.is_open_univ (assume u v _ _, t.is_open_inter u v) (assume k _, t.is_open_sUnion k)‚ü©

/-- If `s` equals the collection of open sets in the topology it generates, then `s` defines a
topology. -/
protected def mk_of_closure (s : set (set Œ±)) (hs : {u | generate_open s u} = s) :
  topological_space Œ± :=
{ is_open        := Œª u, u ‚àà s,
  is_open_univ   := hs ‚ñ∏ topological_space.generate_open.univ,
  is_open_inter  := hs ‚ñ∏ topological_space.generate_open.inter,
  is_open_sUnion := hs ‚ñ∏ topological_space.generate_open.sUnion }

lemma mk_of_closure_sets {s : set (set Œ±)} {hs : {u | generate_open s u} = s} :
  topological_space.mk_of_closure s hs = topological_space.generate_from s :=
topological_space_eq hs.symm

lemma gc_generate_from (Œ±) :
  galois_connection (Œª t : topological_space Œ±, order_dual.to_dual {s | is_open[t] s})
    (generate_from ‚àò order_dual.of_dual) :=
Œª _ _, le_generate_from_iff_subset_is_open.symm

/-- The Galois coinsertion between `topological_space Œ±` and `(set (set Œ±))·µí·µà` whose lower part
  sends a topology to its collection of open subsets, and whose upper part sends a collection of
  subsets of Œ± to the topology they generate. -/
def gci_generate_from (Œ± : Type*) :
  galois_coinsertion (Œª t : topological_space Œ±, order_dual.to_dual {s | is_open[t] s})
    (generate_from ‚àò order_dual.of_dual)  :=
{ gc        := gc_generate_from Œ±,
  u_l_le    := assume ts s hs, generate_open.basic s hs,
  choice    := Œªg hg, topological_space.mk_of_closure g
    (subset.antisymm hg $ le_generate_from_iff_subset_is_open.1 $ le_rfl),
  choice_eq := assume s hs, mk_of_closure_sets }

/-- Topologies on `Œ±` form a complete lattice, with `‚ä•` the discrete topology
  and `‚ä§` the indiscrete topology. The infimum of a collection of topologies
  is the topology generated by all their open sets, while the supremum is the
  topology whose open sets are those sets open in every member of the collection. -/
instance : complete_lattice (topological_space Œ±) :=
(gci_generate_from Œ±).lift_complete_lattice

@[mono] lemma generate_from_anti {Œ±} {g‚ÇÅ g‚ÇÇ : set (set Œ±)} (h : g‚ÇÅ ‚äÜ g‚ÇÇ) :
  generate_from g‚ÇÇ ‚â§ generate_from g‚ÇÅ :=
(gc_generate_from _).monotone_u h

lemma generate_from_set_of_is_open (t : topological_space Œ±) :
  generate_from {s | is_open[t] s} = t :=
(gci_generate_from Œ±).u_l_eq t

lemma left_inverse_generate_from :
  left_inverse generate_from (Œª t : topological_space Œ±, {s | is_open[t] s}) :=
(gci_generate_from Œ±).u_l_left_inverse

lemma generate_from_surjective :
  surjective (generate_from : set (set Œ±) ‚Üí topological_space Œ±) :=
(gci_generate_from Œ±).u_surjective

lemma set_of_is_open_injective : injective (Œª t : topological_space Œ±, {s | is_open[t] s}) :=
(gci_generate_from Œ±).l_injective

end lattice

end topological_space

section lattice

variables {Œ± : Type u} {Œ≤ : Type v}

lemma is_open.mono {Œ±} {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {s : set Œ±} (hs : is_open[t‚ÇÇ] s)
  (h : t‚ÇÅ ‚â§ t‚ÇÇ) : is_open[t‚ÇÅ] s := h s hs

lemma is_closed.mono {Œ±} {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {s : set Œ±} (hs : is_closed[t‚ÇÇ] s)
  (h : t‚ÇÅ ‚â§ t‚ÇÇ) : is_closed[t‚ÇÅ] s :=
(@is_open_compl_iff Œ± t‚ÇÅ s).mp $ hs.is_open_compl.mono h

lemma is_open_implies_is_open_iff {a b : topological_space Œ±} :
  (‚àÄ s, is_open[a] s ‚Üí is_open[b] s) ‚Üî b ‚â§ a :=
iff.rfl

/-- The only open sets in the indiscrete topology are the empty set and the whole space. -/
lemma topological_space.is_open_top_iff {Œ±} (U : set Œ±) :
  is_open[‚ä§] U ‚Üî U = ‚àÖ ‚à® U = univ :=
‚ü®Œª h, begin
  induction h with V h _ _ _ _ ih‚ÇÅ ih‚ÇÇ _ _ ih,
  { cases h }, { exact or.inr rfl },
  { obtain ‚ü®rfl|rfl, rfl|rfl‚ü© := ‚ü®ih‚ÇÅ, ih‚ÇÇ‚ü©; simp },
  { rw [sUnion_eq_empty, or_iff_not_imp_left],
    intro h, push_neg at h, obtain ‚ü®U, hU, hne‚ü© := h,
    have := (ih U hU).resolve_left hne, subst this,
    refine sUnion_eq_univ_iff.2 (Œª a, ‚ü®_, hU, trivial‚ü©) },
end, by { rintro (rfl|rfl), exacts [@is_open_empty _ ‚ä§, @is_open_univ _ ‚ä§] }‚ü©

/-- A topological space is discrete if every set is open, that is,
  its topology equals the discrete topology `‚ä•`. -/
class discrete_topology (Œ± : Type*) [t : topological_space Œ±] : Prop :=
(eq_bot [] : t = ‚ä•)

lemma discrete_topology_bot (Œ± : Type*) : @discrete_topology Œ± ‚ä• := @discrete_topology.mk Œ± ‚ä• rfl

@[simp] lemma is_open_discrete [topological_space Œ±] [discrete_topology Œ±] (s : set Œ±) :
  is_open s :=
(discrete_topology.eq_bot Œ±).symm ‚ñ∏ trivial

@[simp] lemma is_closed_discrete [topological_space Œ±] [discrete_topology Œ±] (s : set Œ±) :
  is_closed s :=
is_open_compl_iff.1 $ is_open_discrete _

@[nontriviality]
lemma continuous_of_discrete_topology [topological_space Œ±] [discrete_topology Œ±]
  [topological_space Œ≤] {f : Œ± ‚Üí Œ≤} : continuous f :=
continuous_def.2 $ Œª s hs, is_open_discrete _

@[simp] lemma nhds_discrete (Œ± : Type*) [topological_space Œ±] [discrete_topology Œ±] :
  (@nhds Œ± _) = pure :=
le_antisymm (Œª _ s hs, (is_open_discrete s).mem_nhds hs) pure_le_nhds

lemma mem_nhds_discrete [topological_space Œ±] [discrete_topology Œ±] {x : Œ±} {s : set Œ±} :
  s ‚àà ùìù x ‚Üî x ‚àà s :=
by rw [nhds_discrete, mem_pure]

lemma le_of_nhds_le_nhds {t‚ÇÅ t‚ÇÇ : topological_space Œ±} (h : ‚àÄx, @nhds Œ± t‚ÇÅ x ‚â§ @nhds Œ± t‚ÇÇ x) :
  t‚ÇÅ ‚â§ t‚ÇÇ :=
begin
  intro s,
  rw [@is_open_iff_mem_nhds _ t‚ÇÅ, @is_open_iff_mem_nhds Œ± t‚ÇÇ],
  exact Œª hs a ha, h _ (hs _ ha)
end

lemma eq_of_nhds_eq_nhds {t‚ÇÅ t‚ÇÇ : topological_space Œ±} (h : ‚àÄx, @nhds Œ± t‚ÇÅ x = @nhds Œ± t‚ÇÇ x) :
  t‚ÇÅ = t‚ÇÇ :=
le_antisymm
  (le_of_nhds_le_nhds $ assume x, le_of_eq $ h x)
  (le_of_nhds_le_nhds $ assume x, le_of_eq $ (h x).symm)

lemma eq_bot_of_singletons_open {t : topological_space Œ±} (h : ‚àÄ x, is_open[t] {x}) : t = ‚ä• :=
bot_unique $ Œª s hs, bUnion_of_singleton s ‚ñ∏ is_open_bUnion (Œª x _, h x)

lemma forall_open_iff_discrete {X : Type*} [topological_space X] :
  (‚àÄ s : set X, is_open s) ‚Üî discrete_topology X :=
‚ü®Œª h, ‚ü®eq_bot_of_singletons_open $ Œª _, h _‚ü©, @is_open_discrete _ _‚ü©

lemma singletons_open_iff_discrete {X : Type*} [topological_space X] :
  (‚àÄ a : X, is_open ({a} : set X)) ‚Üî discrete_topology X :=
‚ü®Œª h, ‚ü®eq_bot_of_singletons_open h‚ü©, Œª a _, @is_open_discrete _ _ a _‚ü©

lemma discrete_topology_iff_singleton_mem_nhds [topological_space Œ±] :
  discrete_topology Œ± ‚Üî ‚àÄ x : Œ±, {x} ‚àà ùìù x :=
by simp only [‚Üê singletons_open_iff_discrete, is_open_iff_mem_nhds, mem_singleton_iff, forall_eq]

/-- This lemma characterizes discrete topological spaces as those whose singletons are
neighbourhoods. -/
lemma discrete_topology_iff_nhds [topological_space Œ±] :
  discrete_topology Œ± ‚Üî ‚àÄ x : Œ±, ùìù x = pure x :=
by simp only [discrete_topology_iff_singleton_mem_nhds, ‚Üê nhds_ne_bot.le_pure_iff,
  le_pure_iff]

lemma discrete_topology_iff_nhds_ne [topological_space Œ±] :
  discrete_topology Œ± ‚Üî ‚àÄ x : Œ±, ùìù[‚â†] x = ‚ä• :=
by simp only [discrete_topology_iff_singleton_mem_nhds, nhds_within, inf_principal_eq_bot,
  compl_compl]

end lattice

section galois_connection
variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}

/-- Given `f : Œ± ‚Üí Œ≤` and a topology on `Œ≤`, the induced topology on `Œ±` is the collection of
  sets that are preimages of some open set in `Œ≤`. This is the coarsest topology that
  makes `f` continuous. -/
def topological_space.induced {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (t : topological_space Œ≤) :
  topological_space Œ± :=
{ is_open        := Œªs, ‚àÉ s', is_open s' ‚àß f ‚Åª¬π' s' = s,
  is_open_univ   := ‚ü®univ, is_open_univ, preimage_univ‚ü©,
  is_open_inter  := by rintro s‚ÇÅ s‚ÇÇ ‚ü®s'‚ÇÅ, hs‚ÇÅ, rfl‚ü© ‚ü®s'‚ÇÇ, hs‚ÇÇ, rfl‚ü©;
    exact ‚ü®s'‚ÇÅ ‚à© s'‚ÇÇ, hs‚ÇÅ.inter hs‚ÇÇ, preimage_inter‚ü©,
  is_open_sUnion := assume s h,
  begin
    simp only [classical.skolem] at h,
    cases h with f hf,
    apply exists.intro (‚ãÉ(x : set Œ±) (h : x ‚àà s), f x h),
    simp only [sUnion_eq_bUnion, preimage_Union, (Œªx h, (hf x h).right)], refine ‚ü®_, rfl‚ü©,
    exact (@is_open_Union Œ≤ _ t _ $ assume i,
      show is_open (‚ãÉh, f i h), from @is_open_Union Œ≤ _ t _ $ assume h, (hf i h).left)
  end }

lemma is_open_induced_iff [t : topological_space Œ≤] {s : set Œ±} {f : Œ± ‚Üí Œ≤} :
  is_open[t.induced f] s ‚Üî (‚àÉt, is_open t ‚àß f ‚Åª¬π' t = s) :=
iff.rfl

lemma is_closed_induced_iff [t : topological_space Œ≤] {s : set Œ±} {f : Œ± ‚Üí Œ≤} :
  is_closed[t.induced f] s ‚Üî (‚àÉt, is_closed t ‚àß f ‚Åª¬π' t = s) :=
begin
  simp only [‚Üê is_open_compl_iff, is_open_induced_iff],
  exact compl_surjective.exists.trans (by simp only [preimage_compl, compl_inj_iff])
end

/-- Given `f : Œ± ‚Üí Œ≤` and a topology on `Œ±`, the coinduced topology on `Œ≤` is defined
  such that `s:set Œ≤` is open if the preimage of `s` is open. This is the finest topology that
  makes `f` continuous. -/
def topological_space.coinduced {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (t : topological_space Œ±) :
  topological_space Œ≤ :=
{ is_open        := Œª s, is_open[t] (f ‚Åª¬π' s),
  is_open_univ   := t.is_open_univ,
  is_open_inter  := Œª _ _ h‚ÇÅ h‚ÇÇ, h‚ÇÅ.inter h‚ÇÇ,
  is_open_sUnion := Œª s h, by simpa only [preimage_sUnion] using is_open_bUnion h }

lemma is_open_coinduced {t : topological_space Œ±} {s : set Œ≤} {f : Œ± ‚Üí Œ≤} :
  is_open[t.coinduced f] s ‚Üî is_open (f ‚Åª¬π' s) :=
iff.rfl

lemma preimage_nhds_coinduced [topological_space Œ±] {œÄ : Œ± ‚Üí Œ≤} {s : set Œ≤}
  {a : Œ±} (hs : s ‚àà @nhds Œ≤ (topological_space.coinduced œÄ ‚Äπ_‚Ä∫) (œÄ a)) : œÄ ‚Åª¬π' s ‚àà ùìù a :=
begin
  letI := topological_space.coinduced œÄ ‚Äπ_‚Ä∫,
  rcases mem_nhds_iff.mp hs with ‚ü®V, hVs, V_op, mem_V‚ü©,
  exact mem_nhds_iff.mpr ‚ü®œÄ ‚Åª¬π' V, set.preimage_mono hVs, V_op, mem_V‚ü©
end

variables {t t‚ÇÅ t‚ÇÇ : topological_space Œ±} {t' : topological_space Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}

lemma continuous.coinduced_le (h : @continuous Œ± Œ≤ t t' f) :
  t.coinduced f ‚â§ t' :=
Œª s hs, (continuous_def.1 h s hs : _)

lemma coinduced_le_iff_le_induced {f : Œ± ‚Üí Œ≤} {tŒ± : topological_space Œ±}
  {tŒ≤ : topological_space Œ≤} :
  tŒ±.coinduced f ‚â§ tŒ≤ ‚Üî tŒ± ‚â§ tŒ≤.induced f :=
‚ü®Œª h s ‚ü®t, ht, hst‚ü©, hst ‚ñ∏ h _ ht, Œª h s hs, h _ ‚ü®s, hs, rfl‚ü©‚ü©

lemma continuous.le_induced (h : @continuous Œ± Œ≤ t t' f) :
  t ‚â§ t'.induced f :=
coinduced_le_iff_le_induced.1 h.coinduced_le

lemma gc_coinduced_induced (f : Œ± ‚Üí Œ≤) :
  galois_connection (topological_space.coinduced f) (topological_space.induced f) :=
assume f g, coinduced_le_iff_le_induced

lemma induced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.induced g ‚â§ t‚ÇÇ.induced g :=
(gc_coinduced_induced g).monotone_u h

lemma coinduced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.coinduced f ‚â§ t‚ÇÇ.coinduced f :=
(gc_coinduced_induced f).monotone_l h

@[simp] lemma induced_top : (‚ä§ : topological_space Œ±).induced g = ‚ä§ :=
(gc_coinduced_induced g).u_top

@[simp] lemma induced_inf : (t‚ÇÅ ‚äì t‚ÇÇ).induced g = t‚ÇÅ.induced g ‚äì t‚ÇÇ.induced g :=
(gc_coinduced_induced g).u_inf

@[simp] lemma induced_infi {Œπ : Sort w} {t : Œπ ‚Üí topological_space Œ±} :
  (‚®Öi, t i).induced g = (‚®Öi, (t i).induced g) :=
(gc_coinduced_induced g).u_infi

@[simp] lemma coinduced_bot : (‚ä• : topological_space Œ±).coinduced f = ‚ä• :=
(gc_coinduced_induced f).l_bot

@[simp] lemma coinduced_sup : (t‚ÇÅ ‚äî t‚ÇÇ).coinduced f = t‚ÇÅ.coinduced f ‚äî t‚ÇÇ.coinduced f :=
(gc_coinduced_induced f).l_sup

@[simp] lemma coinduced_supr {Œπ : Sort w} {t : Œπ ‚Üí topological_space Œ±} :
  (‚®Üi, t i).coinduced f = (‚®Üi, (t i).coinduced f) :=
(gc_coinduced_induced f).l_supr

lemma induced_id [t : topological_space Œ±] : t.induced id = t :=
topological_space_eq $ funext $ assume s, propext $
  ‚ü®assume ‚ü®s', hs, h‚ü©, h ‚ñ∏ hs, assume hs, ‚ü®s, hs, rfl‚ü©‚ü©

lemma induced_compose [tŒ≥ : topological_space Œ≥]
  {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} : (tŒ≥.induced g).induced f = tŒ≥.induced (g ‚àò f) :=
topological_space_eq $ funext $ assume s, propext $
  ‚ü®assume ‚ü®s', ‚ü®s, hs, h‚ÇÇ‚ü©, h‚ÇÅ‚ü©, h‚ÇÅ ‚ñ∏ h‚ÇÇ ‚ñ∏ ‚ü®s, hs, rfl‚ü©,
    assume ‚ü®s, hs, h‚ü©, ‚ü®preimage g s, ‚ü®s, hs, rfl‚ü©, h ‚ñ∏ rfl‚ü©‚ü©

lemma induced_const [t : topological_space Œ±] {x : Œ±} :
  t.induced (Œª y : Œ≤, x) = ‚ä§ :=
le_antisymm le_top (@continuous_const Œ≤ Œ± ‚ä§ t x).le_induced

lemma coinduced_id [t : topological_space Œ±] : t.coinduced id = t :=
topological_space_eq rfl

lemma coinduced_compose [tŒ± : topological_space Œ±]
  {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} : (tŒ±.coinduced f).coinduced g = tŒ±.coinduced (g ‚àò f) :=
topological_space_eq rfl

lemma equiv.induced_symm {Œ± Œ≤ : Type*} (e : Œ± ‚âÉ Œ≤) :
  topological_space.induced e.symm = topological_space.coinduced e :=
begin
  ext t U,
  split,
  { rintros ‚ü®V, hV, rfl‚ü©,
    rwa [is_open_coinduced, e.preimage_symm_preimage] },
  { exact Œª hU, ‚ü®e ‚Åª¬π' U, hU, e.symm_preimage_preimage _‚ü© }
end

lemma equiv.coinduced_symm {Œ± Œ≤ : Type*} (e : Œ± ‚âÉ Œ≤) :
  topological_space.coinduced e.symm = topological_space.induced e :=
by rw [‚Üê e.symm.induced_symm, e.symm_symm]

end galois_connection

/- constructions using the complete lattice structure -/
section constructions
open topological_space

variables {Œ± : Type u} {Œ≤ : Type v}

instance inhabited_topological_space {Œ± : Type u} : inhabited (topological_space Œ±) :=
‚ü®‚ä•‚ü©

@[priority 100]
instance subsingleton.unique_topological_space [subsingleton Œ±] :
  unique (topological_space Œ±) :=
{ default := ‚ä•,
  uniq := Œª t, eq_bot_of_singletons_open $ Œª x, subsingleton.set_cases
    (@is_open_empty _ t) (@is_open_univ _ t) ({x} : set Œ±) }

@[priority 100]
instance subsingleton.discrete_topology [t : topological_space Œ±] [subsingleton Œ±] :
  discrete_topology Œ± :=
‚ü®unique.eq_default t‚ü©

instance : topological_space empty := ‚ä•
instance : discrete_topology empty := ‚ü®rfl‚ü©
instance : topological_space pempty := ‚ä•
instance : discrete_topology pempty := ‚ü®rfl‚ü©
instance : topological_space punit := ‚ä•
instance : discrete_topology punit := ‚ü®rfl‚ü©
instance : topological_space bool := ‚ä•
instance : discrete_topology bool := ‚ü®rfl‚ü©
instance : topological_space ‚Ñï := ‚ä•
instance : discrete_topology ‚Ñï := ‚ü®rfl‚ü©
instance : topological_space ‚Ñ§ := ‚ä•
instance : discrete_topology ‚Ñ§ := ‚ü®rfl‚ü©

instance {n} : topological_space (fin n) := ‚ä•
instance {n} : discrete_topology (fin n) := ‚ü®rfl‚ü©

instance sierpinski_space : topological_space Prop :=
generate_from {{true}}

lemma continuous_empty_function [topological_space Œ±] [topological_space Œ≤] [is_empty Œ≤]
  (f : Œ± ‚Üí Œ≤) : continuous f :=
by { letI := function.is_empty f, exact continuous_of_discrete_topology }

lemma le_generate_from {t : topological_space Œ±} { g : set (set Œ±) } (h : ‚àÄs‚ààg, is_open s) :
  t ‚â§ generate_from g :=
le_generate_from_iff_subset_is_open.2 h

lemma induced_generate_from_eq {Œ± Œ≤} {b : set (set Œ≤)} {f : Œ± ‚Üí Œ≤} :
  (generate_from b).induced f = topological_space.generate_from (preimage f '' b) :=
le_antisymm
  (le_generate_from $ ball_image_iff.2 $ assume s hs, ‚ü®s, generate_open.basic _ hs, rfl‚ü©)
  (coinduced_le_iff_le_induced.1 $ le_generate_from $ assume s hs,
    generate_open.basic _ $ mem_image_of_mem _ hs)

lemma le_induced_generate_from {Œ± Œ≤} [t : topological_space Œ±] {b : set (set Œ≤)}
  {f : Œ± ‚Üí Œ≤} (h : ‚àÄ (a : set Œ≤), a ‚àà b ‚Üí is_open (f ‚Åª¬π' a)) : t ‚â§ induced f (generate_from b) :=
begin
  rw induced_generate_from_eq,
  apply le_generate_from,
  simp only [mem_image, and_imp, forall_apply_eq_imp_iff‚ÇÇ, exists_imp_distrib],
  exact h,
end

/-- This construction is left adjoint to the operation sending a topology on `Œ±`
  to its neighborhood filter at a fixed point `a : Œ±`. -/
def nhds_adjoint (a : Œ±) (f : filter Œ±) : topological_space Œ± :=
{ is_open        := Œªs, a ‚àà s ‚Üí s ‚àà f,
  is_open_univ   := assume s, univ_mem,
  is_open_inter  := assume s t hs ht ‚ü®has, hat‚ü©, inter_mem (hs has) (ht hat),
  is_open_sUnion := assume k hk ‚ü®u, hu, hau‚ü©, mem_of_superset (hk u hu hau)
    (subset_sUnion_of_mem hu) }

lemma gc_nhds (a : Œ±) :
  galois_connection (nhds_adjoint a) (Œªt, @nhds Œ± t a) :=
assume f t, by { rw le_nhds_iff, exact ‚ü®Œª H s hs has, H _ has hs, Œª H s has hs, H _ hs has‚ü© }

lemma nhds_mono {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {a : Œ±} (h : t‚ÇÅ ‚â§ t‚ÇÇ) :
  @nhds Œ± t‚ÇÅ a ‚â§ @nhds Œ± t‚ÇÇ a := (gc_nhds a).monotone_u h

lemma le_iff_nhds {Œ± : Type*} (t t' : topological_space Œ±) :
  t ‚â§ t' ‚Üî ‚àÄ x, @nhds Œ± t x ‚â§ @nhds Œ± t' x :=
‚ü®Œª h x, nhds_mono h, le_of_nhds_le_nhds‚ü©

lemma nhds_adjoint_nhds {Œ± : Type*} (a : Œ±) (f : filter Œ±) :
  @nhds Œ± (nhds_adjoint a f) a = pure a ‚äî f :=
begin
  ext U,
  rw mem_nhds_iff,
  split,
  { rintros ‚ü®t, htU, ht, hat‚ü©,
    exact ‚ü®htU hat, mem_of_superset (ht hat) htU‚ü©},
  { rintros ‚ü®haU, hU‚ü©,
    exact ‚ü®U, subset.rfl, Œª h, hU, haU‚ü© }
end

lemma nhds_adjoint_nhds_of_ne {Œ± : Type*} (a : Œ±) (f : filter Œ±) {b : Œ±} (h : b ‚â† a) :
  @nhds Œ± (nhds_adjoint a f) b = pure b :=
begin
  apply le_antisymm,
  { intros U hU,
    rw mem_nhds_iff,
    use {b},
    simp only [and_true, singleton_subset_iff, mem_singleton],
    refine ‚ü®hU, Œª ha, (h.symm ha).elim‚ü© },
  { exact @pure_le_nhds Œ± (nhds_adjoint a f) b },
end

lemma is_open_singleton_nhds_adjoint {Œ± : Type*} {a b : Œ±} (f : filter Œ±) (hb : b ‚â† a) :
  is_open[nhds_adjoint a f] {b} :=
begin
  rw is_open_singleton_iff_nhds_eq_pure,
  exact nhds_adjoint_nhds_of_ne a f hb
end

lemma le_nhds_adjoint_iff' {Œ± : Type*} (a : Œ±) (f : filter Œ±) (t : topological_space Œ±) :
  t ‚â§ nhds_adjoint a f ‚Üî @nhds Œ± t a ‚â§ pure a ‚äî f ‚àß ‚àÄ b ‚â† a, @nhds Œ± t b = pure b :=
begin
  rw le_iff_nhds,
  split,
  { intros h,
    split,
    { specialize h a,
      rwa nhds_adjoint_nhds at h },
    { intros b hb,
      apply le_antisymm _ (pure_le_nhds b),
      specialize h b,
      rwa nhds_adjoint_nhds_of_ne a f hb at h } },
  { rintros ‚ü®h, h'‚ü© b,
    by_cases hb : b = a,
    { rwa [hb, nhds_adjoint_nhds] },
    { simp [nhds_adjoint_nhds_of_ne a f hb, h' b hb] } }
end

lemma le_nhds_adjoint_iff {Œ± : Type*} (a : Œ±) (f : filter Œ±) (t : topological_space Œ±) :
  t ‚â§ nhds_adjoint a f ‚Üî (@nhds Œ± t a ‚â§ pure a ‚äî f ‚àß ‚àÄ b, b ‚â† a ‚Üí is_open[t] {b}) :=
begin
  change _ ‚Üî _ ‚àß ‚àÄ (b : Œ±), b ‚â† a ‚Üí is_open {b},
  rw [le_nhds_adjoint_iff', and.congr_right_iff],
  apply Œª h, forall_congr (Œª b,  _),
  rw @is_open_singleton_iff_nhds_eq_pure Œ± t b
end

lemma nhds_infi {Œπ : Sort*} {t : Œπ ‚Üí topological_space Œ±} {a : Œ±} :
  @nhds Œ± (infi t) a = (‚®Öi, @nhds Œ± (t i) a) := (gc_nhds a).u_infi

lemma nhds_Inf {s : set (topological_space Œ±)} {a : Œ±} :
  @nhds Œ± (Inf s) a = (‚®Öt‚ààs, @nhds Œ± t a) := (gc_nhds a).u_Inf

lemma nhds_inf {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {a : Œ±} :
  @nhds Œ± (t‚ÇÅ ‚äì t‚ÇÇ) a = @nhds Œ± t‚ÇÅ a ‚äì @nhds Œ± t‚ÇÇ a := (gc_nhds a).u_inf

lemma nhds_top {a : Œ±} : @nhds Œ± ‚ä§ a = ‚ä§ := (gc_nhds a).u_top

lemma is_open_sup {t‚ÇÅ t‚ÇÇ : topological_space Œ±} {s : set Œ±} :
  is_open[t‚ÇÅ ‚äî t‚ÇÇ] s ‚Üî is_open[t‚ÇÅ] s ‚àß is_open[t‚ÇÇ] s :=
iff.rfl

local notation `cont` := @continuous _ _
local notation `tspace` := topological_space
open topological_space

variables {Œ≥ : Type*} {f : Œ± ‚Üí Œ≤} {Œπ : Sort*}

lemma continuous_iff_coinduced_le {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üî coinduced f t‚ÇÅ ‚â§ t‚ÇÇ :=
continuous_def.trans iff.rfl

lemma continuous_iff_le_induced {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üî t‚ÇÅ ‚â§ induced f t‚ÇÇ :=
iff.trans continuous_iff_coinduced_le (gc_coinduced_induced f _ _)

theorem continuous_generated_from {t : tspace Œ±} {b : set (set Œ≤)}
  (h : ‚àÄs‚ààb, is_open (f ‚Åª¬π' s)) : cont t (generate_from b) f :=
continuous_iff_coinduced_le.2 $ le_generate_from h

@[continuity]
lemma continuous_induced_dom {t : tspace Œ≤} : cont (induced f t) t f :=
by { rw continuous_def, assume s h, exact ‚ü®_, h, rfl‚ü© }

lemma continuous_induced_rng {g : Œ≥ ‚Üí Œ±} {t‚ÇÇ : tspace Œ≤} {t‚ÇÅ : tspace Œ≥} :
  cont t‚ÇÅ (induced f t‚ÇÇ) g ‚Üî cont t‚ÇÅ t‚ÇÇ (f ‚àò g) :=
by simp only [continuous_iff_le_induced, induced_compose]

lemma continuous_coinduced_rng {t : tspace Œ±} : cont t (coinduced f t) f :=
by { rw continuous_def, assume s h, exact h }

lemma continuous_coinduced_dom {g : Œ≤ ‚Üí Œ≥} {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : tspace Œ≥} :
  cont (coinduced f t‚ÇÅ) t‚ÇÇ g ‚Üî cont t‚ÇÅ t‚ÇÇ (g ‚àò f) :=
by simp only [continuous_iff_coinduced_le, coinduced_compose]

lemma continuous_le_dom {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÅ) (h‚ÇÇ : cont t‚ÇÅ t‚ÇÉ f) : cont t‚ÇÇ t‚ÇÉ f :=
begin
  rw continuous_def at h‚ÇÇ ‚ä¢,
  assume s h,
  exact h‚ÇÅ _ (h‚ÇÇ s h)
end

lemma continuous_le_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ t‚ÇÉ : tspace Œ≤}
  (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÉ) (h‚ÇÇ : cont t‚ÇÅ t‚ÇÇ f) : cont t‚ÇÅ t‚ÇÉ f :=
begin
  rw continuous_def at h‚ÇÇ ‚ä¢,
  assume s h,
  exact h‚ÇÇ s (h‚ÇÅ s h)
end

lemma continuous_sup_dom {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤} :
  cont (t‚ÇÅ ‚äî t‚ÇÇ) t‚ÇÉ f ‚Üî cont t‚ÇÅ t‚ÇÉ f ‚àß cont t‚ÇÇ t‚ÇÉ f :=
by simp only [continuous_iff_le_induced, sup_le_iff]

lemma continuous_sup_rng_left {t‚ÇÅ : tspace Œ±} {t‚ÇÉ t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÇ f ‚Üí cont t‚ÇÅ (t‚ÇÇ ‚äî t‚ÇÉ) f :=
continuous_le_rng le_sup_left

lemma continuous_sup_rng_right {t‚ÇÅ : tspace Œ±} {t‚ÇÉ t‚ÇÇ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÉ f ‚Üí cont t‚ÇÅ (t‚ÇÇ ‚äî t‚ÇÉ) f :=
continuous_le_rng le_sup_right

lemma continuous_Sup_dom {T : set (tspace Œ±)} {t‚ÇÇ : tspace Œ≤} :
  cont (Sup T) t‚ÇÇ f ‚Üî ‚àÄ t ‚àà T, cont t t‚ÇÇ f :=
by simp only [continuous_iff_le_induced, Sup_le_iff]

lemma continuous_Sup_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : set (tspace Œ≤)} {t : tspace Œ≤}
  (h‚ÇÅ : t ‚àà t‚ÇÇ) (hf : cont t‚ÇÅ t f) : cont t‚ÇÅ (Sup t‚ÇÇ) f :=
continuous_iff_coinduced_le.2 $ le_Sup_of_le h‚ÇÅ $ continuous_iff_coinduced_le.1 hf

lemma continuous_supr_dom {t‚ÇÅ : Œπ ‚Üí tspace Œ±} {t‚ÇÇ : tspace Œ≤} :
  cont (supr t‚ÇÅ) t‚ÇÇ f ‚Üî  ‚àÄ i, cont (t‚ÇÅ i) t‚ÇÇ f :=
by simp only [continuous_iff_le_induced, supr_le_iff]

lemma continuous_supr_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : Œπ ‚Üí tspace Œ≤} {i : Œπ}
  (h : cont t‚ÇÅ (t‚ÇÇ i) f) : cont t‚ÇÅ (supr t‚ÇÇ) f :=
continuous_Sup_rng ‚ü®i, rfl‚ü© h

lemma continuous_inf_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ t‚ÇÉ : tspace Œ≤} :
  cont t‚ÇÅ (t‚ÇÇ ‚äì t‚ÇÉ) f ‚Üî cont t‚ÇÅ t‚ÇÇ f ‚àß cont t‚ÇÅ t‚ÇÉ f :=
by simp only [continuous_iff_coinduced_le, le_inf_iff]

lemma continuous_inf_dom_left {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤} :
  cont t‚ÇÅ t‚ÇÉ f ‚Üí cont (t‚ÇÅ ‚äì t‚ÇÇ) t‚ÇÉ f :=
continuous_le_dom inf_le_left

lemma continuous_inf_dom_right {t‚ÇÅ t‚ÇÇ : tspace Œ±} {t‚ÇÉ : tspace Œ≤} :
  cont t‚ÇÇ t‚ÇÉ f ‚Üí cont (t‚ÇÅ ‚äì t‚ÇÇ) t‚ÇÉ f :=
continuous_le_dom inf_le_right

lemma continuous_Inf_dom {t‚ÇÅ : set (tspace Œ±)} {t‚ÇÇ : tspace Œ≤} {t : tspace Œ±} (h‚ÇÅ : t ‚àà t‚ÇÅ) :
  cont t t‚ÇÇ f ‚Üí cont (Inf t‚ÇÅ) t‚ÇÇ f :=
continuous_le_dom $ Inf_le h‚ÇÅ

lemma continuous_Inf_rng {t‚ÇÅ : tspace Œ±} {T : set (tspace Œ≤)} :
  cont t‚ÇÅ (Inf T) f ‚Üî ‚àÄ t ‚àà T, cont t‚ÇÅ t f :=
by simp only [continuous_iff_coinduced_le, le_Inf_iff]

lemma continuous_infi_dom {t‚ÇÅ : Œπ ‚Üí tspace Œ±} {t‚ÇÇ : tspace Œ≤} {i : Œπ} :
  cont (t‚ÇÅ i) t‚ÇÇ f ‚Üí cont (infi t‚ÇÅ) t‚ÇÇ f :=
continuous_le_dom $ infi_le _ _

lemma continuous_infi_rng {t‚ÇÅ : tspace Œ±} {t‚ÇÇ : Œπ ‚Üí tspace Œ≤} :
  cont t‚ÇÅ (infi t‚ÇÇ) f ‚Üî ‚àÄ i, cont t‚ÇÅ (t‚ÇÇ i) f :=
by simp only [continuous_iff_coinduced_le, le_infi_iff]

@[continuity] lemma continuous_bot {t : tspace Œ≤} : cont ‚ä• t f :=
continuous_iff_le_induced.2 $ bot_le

@[continuity] lemma continuous_top {t : tspace Œ±} : cont t ‚ä§ f :=
continuous_iff_coinduced_le.2 $ le_top

lemma continuous_id_iff_le {t t' : tspace Œ±} : cont t t' id ‚Üî t ‚â§ t' :=
@continuous_def _ _ t t' id

lemma continuous_id_of_le {t t' : tspace Œ±} (h : t ‚â§ t') : cont t t' id :=
continuous_id_iff_le.2 h

/- ùìù in the induced topology -/

theorem mem_nhds_induced [T : topological_space Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) (s : set Œ≤) :
  s ‚àà @nhds Œ≤ (topological_space.induced f T) a ‚Üî ‚àÉ u ‚àà ùìù (f a), f ‚Åª¬π' u ‚äÜ s :=
begin
  simp only [mem_nhds_iff, is_open_induced_iff, exists_prop, set.mem_set_of_eq],
  split,
  { rintros ‚ü®u, usub, ‚ü®v, openv, ueq‚ü©, au‚ü©,
    exact ‚ü®v, ‚ü®v, set.subset.refl v, openv, by rwa ‚Üêueq at au‚ü©, by rw ueq; exact usub‚ü© },
  rintros ‚ü®u, ‚ü®v, vsubu, openv, amem‚ü©, finvsub‚ü©,
  exact ‚ü®f ‚Åª¬π' v, set.subset.trans (set.preimage_mono vsubu) finvsub, ‚ü®‚ü®v, openv, rfl‚ü©, amem‚ü©‚ü©
end

theorem nhds_induced [T : topological_space Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) :
  @nhds Œ≤ (topological_space.induced f T) a = comap f (ùìù (f a)) :=
by { ext s, rw [mem_nhds_induced, mem_comap] }

lemma induced_iff_nhds_eq [tŒ± : topological_space Œ±] [tŒ≤ : topological_space Œ≤] (f : Œ≤ ‚Üí Œ±) :
tŒ≤ = tŒ±.induced f ‚Üî ‚àÄ b, ùìù b = comap f (ùìù $ f b) :=
‚ü®Œª h a, h.symm ‚ñ∏ nhds_induced f a, Œª h, eq_of_nhds_eq_nhds $ Œª x, by rw [h, nhds_induced]‚ü©

theorem map_nhds_induced_of_surjective [T : topological_space Œ±]
    {f : Œ≤ ‚Üí Œ±} (hf : surjective f) (a : Œ≤) :
  map f (@nhds Œ≤ (topological_space.induced f T) a) = ùìù (f a) :=
by rw [nhds_induced, map_comap_of_surjective hf]

end constructions

section induced
open topological_space
variables {Œ± : Type*} {Œ≤ : Type*}
variables [t : topological_space Œ≤] {f : Œ± ‚Üí Œ≤}

theorem is_open_induced_eq {s : set Œ±} :
  is_open[induced f t] s ‚Üî s ‚àà preimage f '' {s | is_open s} :=
iff.rfl

theorem is_open_induced {s : set Œ≤} (h : is_open s) : is_open[induced f t] (f ‚Åª¬π' s) :=
‚ü®s, h, rfl‚ü©

lemma map_nhds_induced_eq (a : Œ±) : map f (@nhds Œ± (induced f t) a) = ùìù[range f] (f a) :=
by rw [nhds_induced, filter.map_comap, nhds_within]

lemma map_nhds_induced_of_mem {a : Œ±} (h : range f ‚àà ùìù (f a)) :
  map f (@nhds Œ± (induced f t) a) = ùìù (f a) :=
by rw [nhds_induced, filter.map_comap_of_mem h]

lemma closure_induced [t : topological_space Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} :
  a ‚àà @closure Œ± (topological_space.induced f t) s ‚Üî f a ‚àà closure (f '' s) :=
by simp only [mem_closure_iff_frequently, nhds_induced, frequently_comap, mem_image, and_comm]

lemma is_closed_induced_iff' [t : topological_space Œ≤] {f : Œ± ‚Üí Œ≤} {s : set Œ±} :
  is_closed[t.induced f] s ‚Üî ‚àÄ a, f a ‚àà closure (f '' s) ‚Üí a ‚àà s :=
by simp only [‚Üê closure_subset_iff_is_closed, subset_def, closure_induced]

end induced

section sierpinski
variables {Œ± : Type*} [topological_space Œ±]

@[simp] lemma is_open_singleton_true : is_open ({true} : set Prop) :=
topological_space.generate_open.basic _ (mem_singleton _)

@[simp] lemma nhds_true : ùìù true = pure true :=
le_antisymm (le_pure_iff.2 $ is_open_singleton_true.mem_nhds $ mem_singleton _) (pure_le_nhds _)

@[simp] lemma nhds_false : ùìù false = ‚ä§ :=
topological_space.nhds_generate_from.trans $ by simp [@and.comm (_ ‚àà _)]

lemma continuous_Prop {p : Œ± ‚Üí Prop} : continuous p ‚Üî is_open {x | p x} :=
‚ü®assume h : continuous p,
  have is_open (p ‚Åª¬π' {true}),
    from is_open_singleton_true.preimage h,
  by simpa [preimage, eq_true_iff] using this,
  assume h : is_open {x | p x},
  continuous_generated_from $ assume s (hs : s = {true}),
    by simp [hs, preimage, eq_true_iff, h]‚ü©

lemma is_open_iff_continuous_mem {s : set Œ±} : is_open s ‚Üî continuous (Œª x, x ‚àà s) :=
continuous_Prop.symm

end sierpinski

section infi
variables {Œ± : Type u} {Œπ : Sort v}

lemma generate_from_union (a‚ÇÅ a‚ÇÇ : set (set Œ±)) :
  topological_space.generate_from (a‚ÇÅ ‚à™ a‚ÇÇ) =
    topological_space.generate_from a‚ÇÅ ‚äì topological_space.generate_from a‚ÇÇ :=
(topological_space.gc_generate_from Œ±).u_inf

lemma set_of_is_open_sup (t‚ÇÅ t‚ÇÇ : topological_space Œ±) :
  {s | is_open[t‚ÇÅ ‚äî t‚ÇÇ] s} = {s | is_open[t‚ÇÅ] s} ‚à© {s | is_open[t‚ÇÇ] s} :=
rfl

lemma generate_from_Union {f : Œπ ‚Üí set (set Œ±)} :
  topological_space.generate_from (‚ãÉ i, f i) = (‚®Ö i, topological_space.generate_from (f i)) :=
(topological_space.gc_generate_from Œ±).u_infi

lemma set_of_is_open_supr {t : Œπ ‚Üí topological_space Œ±} :
  {s | is_open[‚®Ü i, t i] s} = ‚ãÇ i, {s | is_open[t i] s} :=
(topological_space.gc_generate_from Œ±).l_supr

lemma generate_from_sUnion {S : set (set (set Œ±))} :
  topological_space.generate_from (‚ãÉ‚ÇÄ S) = (‚®Ö s ‚àà S, topological_space.generate_from s) :=
(topological_space.gc_generate_from Œ±).u_Inf

lemma set_of_is_open_Sup {T : set (topological_space Œ±)} :
  {s | is_open[Sup T] s} = ‚ãÇ t ‚àà T, {s | is_open[t] s} :=
(topological_space.gc_generate_from Œ±).l_Sup

lemma generate_from_union_is_open (a b : topological_space Œ±) :
  topological_space.generate_from ({s | is_open[a] s} ‚à™ {s | is_open[b] s}) = a ‚äì b :=
(topological_space.gci_generate_from Œ±).u_inf_l a b

lemma generate_from_Union_is_open (f : Œπ ‚Üí topological_space Œ±) :
  topological_space.generate_from (‚ãÉ i, {s | is_open[f i] s}) = ‚®Ö i, (f i) :=
(topological_space.gci_generate_from Œ±).u_infi_l f

lemma generate_from_inter (a b : topological_space Œ±) :
  topological_space.generate_from ({s | is_open[a] s} ‚à© {s | is_open[b] s}) = a ‚äî b :=
(topological_space.gci_generate_from Œ±).u_sup_l a b

lemma generate_from_Inter (f : Œπ ‚Üí topological_space Œ±) :
  topological_space.generate_from (‚ãÇ i, {s | is_open[f i] s}) = ‚®Ü i, (f i) :=
(topological_space.gci_generate_from Œ±).u_supr_l f

lemma generate_from_Inter_of_generate_from_eq_self (f : Œπ ‚Üí set (set Œ±))
  (hf : ‚àÄ i, {s | is_open[topological_space.generate_from (f i)] s} = f i) :
  topological_space.generate_from (‚ãÇ i, (f i)) = ‚®Ü i, topological_space.generate_from (f i) :=
(topological_space.gci_generate_from Œ±).u_supr_of_lu_eq_self f hf

variables {t : Œπ ‚Üí topological_space Œ±}

lemma is_open_supr_iff {s : set Œ±} : is_open[‚®Ü i, t i] s ‚Üî ‚àÄ i, is_open[t i] s :=
show s ‚àà set_of (is_open[supr t]) ‚Üî s ‚àà {x : set Œ± | ‚àÄ (i : Œπ), is_open[t i] x},
by simp [set_of_is_open_supr]

lemma is_closed_supr_iff {s : set Œ±} : is_closed[‚®Ü i, t i] s ‚Üî ‚àÄ i, is_closed[t i] s :=
by simp [‚Üê is_open_compl_iff, is_open_supr_iff]

end infi
