/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Scott Morrison
-/
import category_theory.limits.exact_functor
import category_theory.limits.preserves.finite
import category_theory.preadditive.biproducts
import category_theory.preadditive.functor_category

/-!
# Additive Functors

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A functor between two preadditive categories is called *additive*
provided that the induced map on hom types is a morphism of abelian
groups.

An additive functor between preadditive categories creates and preserves biproducts.
Conversely, if `F : C ‚•§ D` is a functor between preadditive categories, where `C` has binary
biproducts, and if `F` preserves binary biproducts, then `F` is additive.

We also define the category of bundled additive functors.

# Implementation details

`functor.additive` is a `Prop`-valued class, defined by saying that for every two objects `X` and
`Y`, the map `F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)` is a morphism of abelian groups.

-/

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace category_theory

/-- A functor `F` is additive provided `F.map` is an additive homomorphism. -/
class functor.additive {C D : Type*} [category C] [category D]
  [preadditive C] [preadditive D] (F : C ‚•§ D) : Prop :=
(map_add' : Œ† {X Y : C} {f g : X ‚ü∂ Y}, F.map (f + g) = F.map f + F.map g . obviously)

section preadditive

namespace functor

section
variables {C D : Type*} [category C] [category D] [preadditive C]
  [preadditive D] (F : C ‚•§ D) [functor.additive F]

@[simp]
lemma map_add {X Y : C} {f g : X ‚ü∂ Y} : F.map (f + g) = F.map f + F.map g :=
functor.additive.map_add'

/-- `F.map_add_hom` is an additive homomorphism whose underlying function is `F.map`. -/
@[simps {fully_applied := ff}]
def map_add_hom {X Y : C} : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y) :=
add_monoid_hom.mk' (Œª f, F.map f) (Œª f g, F.map_add)

lemma coe_map_add_hom {X Y : C} : ‚áë(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ _) = @map C _ D _ F X Y := rfl

@[priority 100]
instance preserves_zero_morphisms_of_additive : preserves_zero_morphisms F :=
{ map_zero' := Œª X Y, F.map_add_hom.map_zero }

instance : additive (ùü≠ C) :=
{}

instance {E : Type*} [category E] [preadditive E] (G : D ‚•§ E) [functor.additive G] :
  additive (F ‚ãô G) :=
{}

@[simp]
lemma map_neg {X Y : C} {f : X ‚ü∂ Y} : F.map (-f) = - F.map f :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_neg _

@[simp]
lemma map_sub {X Y : C} {f g : X ‚ü∂ Y} : F.map (f - g) = F.map f - F.map g :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_sub _ _

lemma map_nsmul {X Y : C} {f : X ‚ü∂ Y} {n : ‚Ñï} : F.map (n ‚Ä¢ f) = n ‚Ä¢ F.map f :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_nsmul _ _

-- You can alternatively just use `functor.map_smul` here, with an explicit `(r : ‚Ñ§)` argument.
lemma map_zsmul {X Y : C} {f : X ‚ü∂ Y} {r : ‚Ñ§} : F.map (r ‚Ä¢ f) = r ‚Ä¢ F.map f :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y)).map_zsmul _ _

open_locale big_operators

@[simp]
lemma map_sum {X Y : C} {Œ± : Type*} (f : Œ± ‚Üí (X ‚ü∂ Y)) (s : finset Œ±) :
  F.map (‚àë a in s, f a) = ‚àë a in s, F.map (f a) :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ _).map_sum f s

end

section induced_category
variables {C : Type*} {D : Type*} [category D] [preadditive D] (F : C ‚Üí D)

instance induced_functor_additive : functor.additive (induced_functor F) := {}

end induced_category

instance full_subcategory_inclusion_additive
  {C : Type*} [category C] [preadditive C] (Z : C ‚Üí Prop) :
  (full_subcategory_inclusion Z).additive := {}

section
-- To talk about preservation of biproducts we need to specify universes explicitly.

noncomputable theory

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÇ} D]
  [preadditive C] [preadditive D] (F : C ‚•§ D)

open category_theory.limits
open category_theory.preadditive

@[priority 100]
instance preserves_finite_biproducts_of_additive [additive F] : preserves_finite_biproducts F :=
{ preserves := Œª J _,
  { preserves := Œª f,
    { preserves := Œª b hb, by exactI is_bilimit_of_total _
      begin
        simp_rw [F.map_bicone_œÄ, F.map_bicone_Œπ, ‚Üê F.map_comp, ‚Üê F.map_sum],
        dsimp only [map_bicone_X],
        simp_rw [‚Üê F.map_id],
        refine congr_arg _ (hb.is_limit.hom_ext (Œª j, hb.is_colimit.hom_ext (Œª j', _))),
        cases j, cases j',
        dsimp only [limits.bicone.to_cone_œÄ_app],
        simp [sum_comp, comp_sum, bicone.Œπ_œÄ, comp_dite, dite_comp],
      end } } }

lemma additive_of_preserves_binary_biproducts [has_binary_biproducts C] [preserves_zero_morphisms F]
  [preserves_binary_biproducts F] : additive F :=
{ map_add' := Œª X Y f g, by rw [biprod.add_eq_lift_id_desc, F.map_comp, ‚Üê biprod.lift_map_biprod,
    ‚Üê biprod.map_biprod_hom_desc, category.assoc, iso.inv_hom_id_assoc, F.map_id,
    biprod.add_eq_lift_id_desc] }

end

end functor

namespace equivalence

variables {C D : Type*} [category C] [category D] [preadditive C] [preadditive D]

instance inverse_additive (e : C ‚âå D) [e.functor.additive] : e.inverse.additive :=
{ map_add' := Œª X Y f g, by { apply e.functor.map_injective, simp, }, }

end equivalence

section
variables (C D : Type*) [category C] [category D] [preadditive C] [preadditive D]

/-- Bundled additive functors. -/
@[derive category, nolint has_nonempty_instance]
def AdditiveFunctor :=
full_subcategory (Œª (F : C ‚•§ D), F.additive)

infixr ` ‚•§+ `:26 := AdditiveFunctor

instance : preadditive (C ‚•§+ D) :=
preadditive.induced_category _

/-- An additive functor is in particular a functor. -/
@[derive full, derive faithful]
def AdditiveFunctor.forget : (C ‚•§+ D) ‚•§ (C ‚•§ D) :=
full_subcategory_inclusion _

variables {C D}

/-- Turn an additive functor into an object of the category `AdditiveFunctor C D`. -/
def AdditiveFunctor.of (F : C ‚•§ D) [F.additive] : C ‚•§+ D :=
‚ü®F, infer_instance‚ü©

@[simp]
lemma AdditiveFunctor.of_fst (F : C ‚•§ D) [F.additive] : (AdditiveFunctor.of F).1 = F :=
rfl

@[simp]
lemma AdditiveFunctor.forget_obj (F : C ‚•§+ D) : (AdditiveFunctor.forget C D).obj F = F.1 :=
rfl

lemma AdditiveFunctor.forget_obj_of (F : C ‚•§ D) [F.additive] :
  (AdditiveFunctor.forget C D).obj (AdditiveFunctor.of F) = F :=
rfl

@[simp]
lemma AdditiveFunctor.forget_map (F G : C ‚•§+ D) (Œ± : F ‚ü∂ G) :
  (AdditiveFunctor.forget C D).map Œ± = Œ± :=
rfl

instance : functor.additive (AdditiveFunctor.forget C D) :=
{ map_add' := Œª F G Œ± Œ≤, rfl }

instance (F : C ‚•§+ D) : functor.additive F.1 :=
F.2

end

section exact
open category_theory.limits

variables (C : Type u‚ÇÅ) (D : Type u‚ÇÇ) [category.{v‚ÇÅ} C] [category.{v‚ÇÇ} D] [preadditive C]
variables [preadditive D] [has_zero_object C] [has_zero_object D] [has_binary_biproducts C]

section
local attribute [instance] preserves_binary_biproducts_of_preserves_binary_products
local attribute [instance] preserves_binary_biproducts_of_preserves_binary_coproducts

/-- Turn a left exact functor into an additive functor. -/
@[derive full, derive faithful]
def AdditiveFunctor.of_left_exact : (C ‚•§‚Çó D) ‚•§ (C ‚•§+ D) :=
full_subcategory.map (Œª F h, let hF := classical.choice h in
    by exactI functor.additive_of_preserves_binary_biproducts F)

/-- Turn a right exact functor into an additive functor. -/
@[derive full, derive faithful]
def AdditiveFunctor.of_right_exact : (C ‚•§·µ£ D) ‚•§ (C ‚•§+ D) :=
full_subcategory.map (Œª F h, let hF := classical.choice h in
  by exactI functor.additive_of_preserves_binary_biproducts F)

/-- Turn an exact functor into an additive functor. -/
@[derive full, derive faithful]
def AdditiveFunctor.of_exact : (C ‚•§‚Çë D) ‚•§ (C ‚•§+ D) :=
full_subcategory.map (Œª F h, let hF := classical.choice h.1 in
  by exactI functor.additive_of_preserves_binary_biproducts F)

end

variables {C D}

@[simp] lemma AdditiveFunctor.of_left_exact_obj_fst (F : C ‚•§‚Çó D) :
  ((AdditiveFunctor.of_left_exact C D).obj F).obj = F.obj := rfl
@[simp] lemma AdditiveFunctor.of_right_exact_obj_fst (F : C ‚•§·µ£ D) :
  ((AdditiveFunctor.of_right_exact C D).obj F).obj = F.obj := rfl
@[simp] lemma AdditiveFunctor.of_exact_obj_fst (F : C ‚•§‚Çë D) :
  ((AdditiveFunctor.of_exact C D).obj F).obj = F.obj := rfl

@[simp] lemma Additive_Functor.of_left_exact_map {F G : C ‚•§‚Çó D} (Œ± : F ‚ü∂ G) :
  (AdditiveFunctor.of_left_exact C D).map Œ± = Œ± := rfl
@[simp] lemma Additive_Functor.of_right_exact_map {F G : C ‚•§·µ£ D} (Œ± : F ‚ü∂ G) :
  (AdditiveFunctor.of_right_exact C D).map Œ± = Œ± := rfl
@[simp] lemma Additive_Functor.of_exact_map {F G : C ‚•§‚Çë D} (Œ± : F ‚ü∂ G) :
  (AdditiveFunctor.of_exact C D).map Œ± = Œ± := rfl

end exact

end preadditive

end category_theory
