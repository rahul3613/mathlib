/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.eq_to_hom
import data.ulift

/-!
# Discrete categories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define `discrete Œ±` as a structure containing a term `a : Œ±` for any type `Œ±`,
and use this type alias to provide a `small_category` instance
whose only morphisms are the identities.

There is an annoying technical difficulty that it has turned out to be inconvenient
to allow categories with morphisms living in `Prop`,
so instead of defining `X ‚ü∂ Y` in `discrete Œ±` as `X = Y`,
one might define it as `plift (X = Y)`.
In fact, to allow `discrete Œ±` to be a `small_category`
(i.e. with morphisms in the same universe as the objects),
we actually define the hom type `X ‚ü∂ Y` as `ulift (plift (X = Y))`.

`discrete.functor` promotes a function `f : I ‚Üí C` (for any category `C`) to a functor
`discrete.functor f : discrete I ‚•§ C`.

Similarly, `discrete.nat_trans` and `discrete.nat_iso` promote `I`-indexed families of morphisms,
or `I`-indexed families of isomorphisms to natural transformations or natural isomorphism.

We show equivalences of types are the same as (categorical) equivalences of the corresponding
discrete categories.
-/

namespace category_theory

-- morphism levels before object levels. See note [category_theory universes].
universes v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÅ' u‚ÇÇ u‚ÇÉ

/--
A wrapper for promoting any type to a category,
with the only morphisms being equalities.
-/
-- This is intentionally a structure rather than a type synonym
-- to enforce using `discrete_equiv` (or `discrete.mk` and `discrete.as`) to move between
-- `discrete Œ±` and `Œ±`. Otherwise there is too much API leakage.
@[ext] structure discrete (Œ± : Type u‚ÇÅ) :=
(as : Œ±)

@[simp] lemma discrete.mk_as {Œ± : Type u‚ÇÅ} (X : discrete Œ±) : discrete.mk X.as = X :=
by { ext, refl, }

/-- `discrete Œ±` is equivalent to the original type `Œ±`.-/
@[simps] def discrete_equiv {Œ± : Type u‚ÇÅ} : discrete Œ± ‚âÉ Œ± :=
{ to_fun := discrete.as,
  inv_fun := discrete.mk,
  left_inv := by tidy,
  right_inv := by tidy, }

instance {Œ± : Type u‚ÇÅ} [decidable_eq Œ±] : decidable_eq (discrete Œ±) :=
discrete_equiv.decidable_eq

/--
The "discrete" category on a type, whose morphisms are equalities.

Because we do not allow morphisms in `Prop` (only in `Type`),
somewhat annoyingly we have to define `X ‚ü∂ Y` as `ulift (plift (X = Y))`.

See <https://stacks.math.columbia.edu/tag/001A>
-/
instance discrete_category (Œ± : Type u‚ÇÅ) : small_category (discrete Œ±) :=
{ hom  := Œª X Y, ulift (plift (X.as = Y.as)),
  id   := Œª X, ulift.up (plift.up rfl),
  comp := Œª X Y Z g f, by { cases X, cases Y, cases Z, rcases f with ‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©, exact g } }

namespace discrete

variables {Œ± : Type u‚ÇÅ}

instance [inhabited Œ±] : inhabited (discrete Œ±) :=
‚ü®‚ü®default‚ü©‚ü©

instance [subsingleton Œ±] : subsingleton (discrete Œ±) :=
‚ü®by { intros, ext, apply subsingleton.elim, }‚ü©

/-- A simple tactic to run `cases` on any `discrete Œ±` hypotheses. -/
meta def _root_.tactic.discrete_cases : tactic unit :=
`[cases_matching* [discrete _, (_ : discrete _) ‚ü∂ (_ : discrete _), plift _]]

run_cmd add_interactive [``tactic.discrete_cases]

local attribute [tidy] tactic.discrete_cases

instance [unique Œ±] : unique (discrete Œ±) :=
unique.mk' (discrete Œ±)

/-- Extract the equation from a morphism in a discrete category. -/
lemma eq_of_hom {X Y : discrete Œ±} (i : X ‚ü∂ Y) : X.as = Y.as := i.down.down

/-- Promote an equation between the wrapped terms in `X Y : discrete Œ±` to a morphism `X ‚ü∂ Y`
in the discrete category. -/
abbreviation eq_to_hom {X Y : discrete Œ±} (h : X.as = Y.as) : X ‚ü∂ Y :=
eq_to_hom (by { ext, exact h, })

/-- Promote an equation between the wrapped terms in `X Y : discrete Œ±` to an isomorphism `X ‚âÖ Y`
in the discrete category. -/
abbreviation eq_to_iso {X Y : discrete Œ±} (h : X.as = Y.as) : X ‚âÖ Y :=
eq_to_iso (by { ext, exact h, })

/-- A variant of `eq_to_hom` that lifts terms to the discrete category. -/
abbreviation eq_to_hom' {a b : Œ±} (h : a = b) : discrete.mk a ‚ü∂ discrete.mk b :=
eq_to_hom h

/-- A variant of `eq_to_iso` that lifts terms to the discrete category. -/
abbreviation eq_to_iso' {a b : Œ±} (h : a = b) : discrete.mk a ‚âÖ discrete.mk b :=
eq_to_iso h

@[simp] lemma id_def (X : discrete Œ±) : ulift.up (plift.up (eq.refl X.as)) = ùüô X := rfl

variables {C : Type u‚ÇÇ} [category.{v‚ÇÇ} C]

instance {I : Type u‚ÇÅ} {i j : discrete I} (f : i ‚ü∂ j) : is_iso f :=
‚ü®‚ü®eq_to_hom (eq_of_hom f).symm, by tidy‚ü©‚ü©

/--
Any function `I ‚Üí C` gives a functor `discrete I ‚•§ C`.
-/
def functor {I : Type u‚ÇÅ} (F : I ‚Üí C) : discrete I ‚•§ C :=
{ obj := F ‚àò discrete.as,
  map := Œª X Y f, by { discrete_cases, cases f, exact ùüô (F X), } }

@[simp] lemma functor_obj  {I : Type u‚ÇÅ} (F : I ‚Üí C) (i : I) :
  (discrete.functor F).obj (discrete.mk i) = F i := rfl

lemma functor_map  {I : Type u‚ÇÅ} (F : I ‚Üí C) {i : discrete I} (f : i ‚ü∂ i) :
  (discrete.functor F).map f = ùüô (F i.as) :=
by tidy

/--
The discrete functor induced by a composition of maps can be written as a
composition of two discrete functors.
-/
@[simps]
def functor_comp {I : Type u‚ÇÅ} {J : Type u‚ÇÅ'} (f : J ‚Üí C) (g : I ‚Üí J) :
  discrete.functor (f ‚àò g) ‚âÖ discrete.functor (discrete.mk ‚àò g) ‚ãô discrete.functor f :=
nat_iso.of_components (Œª X, iso.refl _) (by tidy)

/--
For functors out of a discrete category,
a natural transformation is just a collection of maps,
as the naturality squares are trivial.
-/
@[simps]
def nat_trans {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚ü∂ G.obj i) : F ‚ü∂ G :=
{ app := f,
  naturality' := Œª X Y g, by { discrete_cases, cases g, simp, } }

/--
For functors out of a discrete category,
a natural isomorphism is just a collection of isomorphisms,
as the naturality squares are trivial.
-/
@[simps]
def nat_iso {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) : F ‚âÖ G :=
nat_iso.of_components f (Œª X Y g, by { discrete_cases, cases g, simp, })

@[simp]
lemma nat_iso_app {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) (i : discrete I) :
  (discrete.nat_iso f).app i = f i :=
by tidy

/-- Every functor `F` from a discrete category is naturally isomorphic (actually, equal) to
  `discrete.functor (F.obj)`. -/
@[simp]
def nat_iso_functor {I : Type u‚ÇÅ} {F : discrete I ‚•§ C} :
  F ‚âÖ discrete.functor (F.obj ‚àò discrete.mk) :=
nat_iso $ Œª i, by { discrete_cases, refl, }

/-- Composing `discrete.functor F` with another functor `G` amounts to composing `F` with `G.obj` -/
@[simp]
def comp_nat_iso_discrete {I : Type u‚ÇÅ} {D : Type u‚ÇÉ} [category.{v‚ÇÉ} D]
 (F : I ‚Üí C) (G : C ‚•§ D) : discrete.functor F ‚ãô G ‚âÖ discrete.functor (G.obj ‚àò F) :=
nat_iso $ Œª i, iso.refl _

/--
We can promote a type-level `equiv` to
an equivalence between the corresponding `discrete` categories.
-/
@[simps]
def equivalence {I : Type u‚ÇÅ} {J : Type u‚ÇÇ} (e : I ‚âÉ J) : discrete I ‚âå discrete J :=
{ functor := discrete.functor (discrete.mk ‚àò (e : I ‚Üí J)),
  inverse := discrete.functor (discrete.mk ‚àò (e.symm : J ‚Üí I)),
  unit_iso := discrete.nat_iso (Œª i, eq_to_iso (by { discrete_cases, simp })),
  counit_iso := discrete.nat_iso (Œª j, eq_to_iso (by { discrete_cases, simp })), }

/-- We can convert an equivalence of `discrete` categories to a type-level `equiv`. -/
@[simps]
def equiv_of_equivalence {Œ± : Type u‚ÇÅ} {Œ≤ : Type u‚ÇÇ} (h : discrete Œ± ‚âå discrete Œ≤) : Œ± ‚âÉ Œ≤ :=
{ to_fun := discrete.as ‚àò h.functor.obj ‚àò discrete.mk,
  inv_fun := discrete.as ‚àò h.inverse.obj ‚àò discrete.mk,
  left_inv := Œª a, by simpa using eq_of_hom (h.unit_iso.app (discrete.mk a)).2,
  right_inv := Œª a, by simpa using eq_of_hom (h.counit_iso.app (discrete.mk a)).1, }

end discrete

namespace discrete
variables {J : Type v‚ÇÅ}

open opposite

/-- A discrete category is equivalent to its opposite category. -/
@[simps functor_obj_as inverse_obj]
protected def opposite (Œ± : Type u‚ÇÅ) : (discrete Œ±)·µí·µñ ‚âå discrete Œ± :=
let F : discrete Œ± ‚•§ (discrete Œ±)·µí·µñ := discrete.functor (Œª x, op (discrete.mk x)) in
begin
  refine equivalence.mk (functor.left_op F) F _
    (discrete.nat_iso $ Œª X, by { discrete_cases, simp [F] }),
  refine nat_iso.of_components (Œª X, by { tactic.op_induction', discrete_cases, simp [F], }) _,
  tidy
end

variables {C : Type u‚ÇÇ} [category.{v‚ÇÇ} C]

@[simp] lemma functor_map_id
  (F : discrete J ‚•§ C) {j : discrete J} (f : j ‚ü∂ j) : F.map f = ùüô (F.obj j) :=
begin
  have h : f = ùüô j, { cases f, cases f, ext, },
  rw h,
  simp,
end

end discrete

end category_theory
