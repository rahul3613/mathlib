/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.eq_to_hom
import category_theory.quotient
import combinatorics.quiver.path

/-!
# The category paths on a quiver.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.
When `C` is a quiver, `paths C` is the category of paths.

## When the quiver is itself a category
We provide `path_composition : paths C ‚•§ C`.

We check that the quotient of the path category of a category by the canonical relation
(paths are related if they compose to the same path) is equivalent to the original category.
-/

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace category_theory

section

/--
A type synonym for the category of paths in a quiver.
-/
def paths (V : Type u‚ÇÅ) : Type u‚ÇÅ := V

instance (V : Type u‚ÇÅ) [inhabited V] : inhabited (paths V) := ‚ü®(default : V)‚ü©

variables (V : Type u‚ÇÅ) [quiver.{v‚ÇÅ+1} V]

namespace paths

instance category_paths : category.{max u‚ÇÅ v‚ÇÅ} (paths V) :=
{ hom := Œª (X Y : V), quiver.path X Y,
  id := Œª X, quiver.path.nil,
  comp := Œª X Y Z f g, quiver.path.comp f g, }

variables {V}

/--
The inclusion of a quiver `V` into its path category, as a prefunctor.
-/
@[simps]
def of : V ‚•§q (paths V) :=
{ obj := Œª X, X,
  map := Œª X Y f, f.to_path, }

local attribute [ext] functor.ext

/-- Any prefunctor from `V` lifts to a functor from `paths V` -/
def lift {C} [category C] (œÜ : V ‚•§q C) : paths V ‚•§ C :=
{ obj := œÜ.obj,
  map := Œª X Y f, @quiver.path.rec V _ X (Œª Y f, œÜ.obj X ‚ü∂ œÜ.obj Y) (ùüô $ œÜ.obj X)
                  (Œª Y Z p f ihp, ihp ‚â´ (œÜ.map f)) Y f,
  map_id' := Œª X, by { refl, },
  map_comp' := Œª X Y Z f g, by
  { induction g with _ _ g' p ih _ _ _,
    { rw category.comp_id, refl, },
    { have : f ‚â´ g'.cons p = (f ‚â´ g').cons p, by apply quiver.path.comp_cons,
      rw this, simp only, rw [ih, category.assoc], } } }

@[simp] lemma lift_nil {C} [category C] (œÜ : V ‚•§q C) (X : V) :
  (lift œÜ).map (quiver.path.nil) = ùüô (œÜ.obj X) := rfl

@[simp] lemma lift_cons {C} [category C] (œÜ : V ‚•§q C) {X Y Z : V}
  (p : quiver.path X Y) (f : Y ‚ü∂ Z) :
  (lift œÜ).map (p.cons f) = (lift œÜ).map p ‚â´ (œÜ.map f) := rfl

@[simp] lemma lift_to_path {C} [category C] (œÜ : V ‚•§q C) {X Y : V} (f : X ‚ü∂ Y) :
  (lift œÜ).map f.to_path = œÜ.map f := by {dsimp [quiver.hom.to_path,lift], simp, }

lemma lift_spec {C} [category C] (œÜ : V ‚•§q C) :
  of ‚ãôq (lift œÜ).to_prefunctor = œÜ :=
begin
  apply prefunctor.ext, rotate,
  { rintro X, refl, },
  { rintro X Y f, rcases œÜ with ‚ü®œÜo,œÜm‚ü©,
    dsimp [lift, quiver.hom.to_path],
    simp only [category.id_comp], },
end

lemma lift_unique {C} [category C] (œÜ : V ‚•§q C) (Œ¶ : paths V ‚•§ C)
  (hŒ¶ : of ‚ãôq Œ¶.to_prefunctor = œÜ) : Œ¶ = lift œÜ :=
begin
  subst_vars,
  apply functor.ext, rotate,
  { rintro X, refl, },
  { rintro X Y f,
    dsimp [lift],
    induction f with _ _ p f' ih,
    { simp only [category.comp_id], apply functor.map_id, },
    { simp only [category.comp_id, category.id_comp] at ih ‚ä¢,
      have : Œ¶.map (p.cons f') = Œ¶.map p ‚â´ (Œ¶.map (f'.to_path)), by
      { convert functor.map_comp Œ¶ p (f'.to_path), },
      rw [this, ih], }, },
end

/-- Two functors out of a path category are equal when they agree on singleton paths. -/
@[ext]
lemma ext_functor {C} [category C]
  {F G : paths V ‚•§ C}
  (h_obj : F.obj = G.obj)
  (h : ‚àÄ (a b : V) (e : a ‚ü∂ b), F.map e.to_path =
  eq_to_hom (congr_fun h_obj a) ‚â´ G.map e.to_path ‚â´ eq_to_hom (congr_fun h_obj.symm b)) :
  F = G :=
begin
  ext X Y f,
  { induction f with Y' Z' g e ih,
    { erw [F.map_id, G.map_id, category.id_comp, eq_to_hom_trans, eq_to_hom_refl], },
    { erw [F.map_comp g e.to_path, G.map_comp g e.to_path, ih, h],
      simp only [category.id_comp, eq_to_hom_refl, eq_to_hom_trans_assoc, category.assoc], }, },
  { intro X, rw h_obj, }
end

end paths

variables (W : Type u‚ÇÇ) [quiver.{v‚ÇÇ+1} W]

-- A restatement of `prefunctor.map_path_comp` using `f ‚â´ g` instead of `f.comp g`.
@[simp] lemma prefunctor.map_path_comp' (F : V ‚•§q W) {X Y Z : paths V} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  F.map_path (f ‚â´ g) = (F.map_path f).comp (F.map_path g) :=
prefunctor.map_path_comp _ _ _

end

section

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

open quiver

/-- A path in a category can be composed to a single morphism. -/
@[simp]
def compose_path {X : C} : Œ† {Y : C} (p : path X Y), X ‚ü∂ Y
| _ path.nil := ùüô X
| _ (path.cons p e) := compose_path p ‚â´ e

@[simp]
lemma compose_path_to_path {X Y : C} (f : X ‚ü∂ Y) : compose_path (f.to_path) = f :=
category.id_comp _

@[simp]
lemma compose_path_comp {X Y Z : C} (f : path X Y) (g : path Y Z) :
  compose_path (f.comp g) = compose_path f ‚â´ compose_path g :=
begin
  induction g with Y' Z' g e ih,
  { simp, },
  { simp [ih], },
end

@[simp]
lemma compose_path_id {X : paths C} : compose_path (ùüô X) = ùüô X := rfl

@[simp]
lemma compose_path_comp' {X Y Z : paths C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  compose_path (f ‚â´ g) = compose_path f ‚â´ compose_path g :=
compose_path_comp f g

variables (C)

/-- Composition of paths as functor from the path category of a category to the category. -/
@[simps]
def path_composition : paths C ‚•§ C :=
{ obj := Œª X, X,
  map := Œª X Y f, compose_path f, }

/-- The canonical relation on the path category of a category:
two paths are related if they compose to the same morphism. -/
-- TODO: This, and what follows, should be generalized to
-- the `hom_rel` for the kernel of any functor.
-- Indeed, this should be part of an equivalence between congruence relations on a category `C`
-- and full, essentially surjective functors out of `C`.
@[simp]
def paths_hom_rel : hom_rel (paths C) :=
Œª X Y p q, (path_composition C).map p = (path_composition C).map q

/-- The functor from a category to the canonical quotient of its path category. -/
@[simps]
def to_quotient_paths : C ‚•§ quotient (paths_hom_rel C) :=
{ obj := Œª X, quotient.mk X,
  map := Œª X Y f, quot.mk _ f.to_path,
  map_id' := Œª X, quot.sound (quotient.comp_closure.of _ _ _ (by simp)),
  map_comp' := Œª X Y Z f g, quot.sound (quotient.comp_closure.of _ _ _ (by simp)), }

/-- The functor from the canonical quotient of a path category of a category
to the original category. -/
@[simps]
def quotient_paths_to : quotient (paths_hom_rel C) ‚•§ C :=
quotient.lift _ (path_composition C) (Œª X Y p q w, w)

/-- The canonical quotient of the path category of a category
is equivalent to the original category. -/
def quotient_paths_equiv : quotient (paths_hom_rel C) ‚âå C :=
{ functor := quotient_paths_to C,
  inverse := to_quotient_paths C,
  unit_iso := nat_iso.of_components (Œª X, by { cases X, refl, }) begin
    intros,
    cases X, cases Y,
    induction f,
    dsimp,
    simp only [category.comp_id, category.id_comp],
    apply quot.sound,
    apply quotient.comp_closure.of,
    simp [paths_hom_rel],
  end,
  counit_iso := nat_iso.of_components (Œª X, iso.refl _) (by tidy),
  functor_unit_iso_comp' := by { intros, cases X, dsimp, simp, refl, }, }

end

end category_theory
