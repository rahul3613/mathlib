/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.preserves.shapes.products
import category_theory.limits.shapes.binary_products
import category_theory.limits.shapes.finite_products
import logic.equiv.fin

/-!
# Constructing finite products from binary products and terminal.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

If a category has binary products and a terminal object then it has finite products.
If a functor preserves binary products and the terminal object then it preserves finite products.

# TODO

Provide the dual results.
Show the analogous results for functors which reflect or create (co)limits.
-/

universes v v' u u'

noncomputable theory
open category_theory category_theory.category category_theory.limits
namespace category_theory

variables {J : Type v} [small_category J]
variables {C : Type u} [category.{v} C]
variables {D : Type u'} [category.{v'} D]

/--
Given `n+1` objects of `C`, a fan for the last `n` with point `c‚ÇÅ.X` and a binary fan on `c‚ÇÅ.X` and
`f 0`, we can build a fan for all `n+1`.

In `extend_fan_is_limit` we show that if the two given fans are limits, then this fan is also a
limit.
-/
@[simps {rhs_md := semireducible}]
def extend_fan {n : ‚Ñï} {f : fin (n+1) ‚Üí C}
  (c‚ÇÅ : fan (Œª (i : fin n), f i.succ))
  (c‚ÇÇ : binary_fan (f 0) c‚ÇÅ.X) :
  fan f :=
fan.mk c‚ÇÇ.X
begin
  refine fin.cases _ _,
  { apply c‚ÇÇ.fst },
  { intro i, apply c‚ÇÇ.snd ‚â´ c‚ÇÅ.œÄ.app ‚ü®i‚ü© },
end

/--
Show that if the two given fans in `extend_fan` are limits, then the constructed fan is also a
limit.
-/
def extend_fan_is_limit {n : ‚Ñï} (f : fin (n+1) ‚Üí C)
  {c‚ÇÅ : fan (Œª (i : fin n), f i.succ)} {c‚ÇÇ : binary_fan (f 0) c‚ÇÅ.X}
  (t‚ÇÅ : is_limit c‚ÇÅ) (t‚ÇÇ : is_limit c‚ÇÇ) :
  is_limit (extend_fan c‚ÇÅ c‚ÇÇ) :=
{ lift := Œª s,
  begin
    apply (binary_fan.is_limit.lift' t‚ÇÇ (s.œÄ.app ‚ü®0‚ü©) _).1,
    apply t‚ÇÅ.lift ‚ü®_, discrete.nat_trans (Œª ‚ü®i‚ü©, s.œÄ.app ‚ü®i.succ‚ü©)‚ü©
  end,
  fac' := Œª s ‚ü®j‚ü©,
  begin
    apply fin.induction_on j,
    { apply (binary_fan.is_limit.lift' t‚ÇÇ _ _).2.1 },
    { rintro i -,
      dsimp only [extend_fan_œÄ_app],
      rw [fin.cases_succ, ‚Üê assoc, (binary_fan.is_limit.lift' t‚ÇÇ _ _).2.2, t‚ÇÅ.fac],
      refl }
  end,
  uniq' := Œª s m w,
  begin
    apply binary_fan.is_limit.hom_ext t‚ÇÇ,
    { rw (binary_fan.is_limit.lift' t‚ÇÇ _ _).2.1,
      apply w ‚ü®0‚ü© },
    { rw (binary_fan.is_limit.lift' t‚ÇÇ _ _).2.2,
      apply t‚ÇÅ.uniq ‚ü®_, _‚ü©,
      rintro ‚ü®j‚ü©,
      rw assoc,
      dsimp only [discrete.nat_trans_app, extend_fan_is_limit._match_1],
      rw ‚Üê w ‚ü®j.succ‚ü©,
      dsimp only [extend_fan_œÄ_app],
      rw fin.cases_succ }
  end }

section
variables [has_binary_products C] [has_terminal C]

/--
If `C` has a terminal object and binary products, then it has a product for objects indexed by
`fin n`.
This is a helper lemma for `has_finite_products_of_has_binary_and_terminal`, which is more general
than this.
-/
private lemma has_product_fin :
  Œ† (n : ‚Ñï) (f : fin n ‚Üí C), has_product f
| 0 := Œª f,
  begin
    letI : has_limits_of_shape (discrete (fin 0)) C :=
      has_limits_of_shape_of_equivalence (discrete.equivalence.{0} fin_zero_equiv'.symm),
    apply_instance,
  end
| (n+1) := Œª f,
  begin
    haveI := has_product_fin n,
    apply has_limit.mk ‚ü®_, extend_fan_is_limit f (limit.is_limit _) (limit.is_limit _)‚ü©,
  end

/-- If `C` has a terminal object and binary products, then it has finite products. -/
lemma has_finite_products_of_has_binary_and_terminal : has_finite_products C :=
begin
  refine ‚ü®Œª n, ‚ü®Œª K, _‚ü©‚ü©,
  letI := has_product_fin n (Œª n, K.obj ‚ü®n‚ü©),
  let : discrete.functor (Œª n, K.obj ‚ü®n‚ü©) ‚âÖ K := discrete.nat_iso (Œª ‚ü®i‚ü©, iso.refl _),
  apply has_limit_of_iso this,
end

end

section preserves
variables (F : C ‚•§ D)
variables [preserves_limits_of_shape (discrete walking_pair) F]
variables [preserves_limits_of_shape (discrete.{0} pempty) F]
variables [has_finite_products.{v} C]

/--
If `F` preserves the terminal object and binary products, then it preserves products indexed by
`fin n` for any `n`.
-/
noncomputable def preserves_fin_of_preserves_binary_and_terminal  :
  Œ† (n : ‚Ñï) (f : fin n ‚Üí C), preserves_limit (discrete.functor f) F
| 0 := Œª f,
  begin
    letI : preserves_limits_of_shape (discrete (fin 0)) F :=
      preserves_limits_of_shape_of_equiv.{0 0}
        (discrete.equivalence fin_zero_equiv'.symm) _,
    apply_instance,
  end
| (n+1) :=
  begin
    haveI := preserves_fin_of_preserves_binary_and_terminal n,
    intro f,
    refine preserves_limit_of_preserves_limit_cone
      (extend_fan_is_limit f (limit.is_limit _) (limit.is_limit _)) _,
    apply (is_limit_map_cone_fan_mk_equiv _ _ _).symm _,
    let := extend_fan_is_limit (Œª i, F.obj (f i))
              (is_limit_of_has_product_of_preserves_limit F _)
              (is_limit_of_has_binary_product_of_preserves_limit F _ _),
    refine is_limit.of_iso_limit this _,
    apply cones.ext _ _,
    apply iso.refl _,
    rintro ‚ü®j‚ü©,
    apply fin.induction_on j,
    { apply (category.id_comp _).symm },
    { rintro i -,
      dsimp only [extend_fan_œÄ_app, iso.refl_hom, fan.mk_œÄ_app],
      rw [fin.cases_succ, fin.cases_succ],
      change F.map _ ‚â´ _ = ùüô _ ‚â´ _,
      rw [id_comp, ‚ÜêF.map_comp],
      refl }
  end

/--
If `F` preserves the terminal object and binary products, then it preserves limits of shape
`discrete (fin n)`.
-/
def preserves_shape_fin_of_preserves_binary_and_terminal (n : ‚Ñï) :
  preserves_limits_of_shape (discrete (fin n)) F :=
{ preserves_limit := Œª K,
  begin
    let : discrete.functor (Œª n, K.obj ‚ü®n‚ü©) ‚âÖ K := discrete.nat_iso (Œª ‚ü®i‚ü©, iso.refl _),
    haveI := preserves_fin_of_preserves_binary_and_terminal F n (Œª n, K.obj ‚ü®n‚ü©),
    apply preserves_limit_of_iso_diagram F this,
  end }

/-- If `F` preserves the terminal object and binary products then it preserves finite products. -/
def preserves_finite_products_of_preserves_binary_and_terminal
  (J : Type) [fintype J] :
  preserves_limits_of_shape (discrete J) F :=
begin
  classical,
  let e := fintype.equiv_fin J,
  haveI := preserves_shape_fin_of_preserves_binary_and_terminal F (fintype.card J),
  apply preserves_limits_of_shape_of_equiv.{0 0}
    (discrete.equivalence e).symm,
end

end preserves

/--
Given `n+1` objects of `C`, a cofan for the last `n` with point `c‚ÇÅ.X`
and a binary cofan on `c‚ÇÅ.X` and `f 0`, we can build a cofan for all `n+1`.

In `extend_cofan_is_colimit` we show that if the two given cofans are colimits,
then this cofan is also a colimit.
-/
@[simps {rhs_md := semireducible}]
def extend_cofan {n : ‚Ñï} {f : fin (n+1) ‚Üí C}
  (c‚ÇÅ : cofan (Œª (i : fin n), f i.succ))
  (c‚ÇÇ : binary_cofan (f 0) c‚ÇÅ.X) :
  cofan f :=
cofan.mk c‚ÇÇ.X
begin
  refine fin.cases _ _,
  { apply c‚ÇÇ.inl },
  { intro i,
    apply c‚ÇÅ.Œπ.app ‚ü®i‚ü© ‚â´ c‚ÇÇ.inr },
end

/--
Show that if the two given cofans in `extend_cofan` are colimits,
then the constructed cofan is also a colimit.
-/
def extend_cofan_is_colimit {n : ‚Ñï} (f : fin (n+1) ‚Üí C)
  {c‚ÇÅ : cofan (Œª (i : fin n), f i.succ)} {c‚ÇÇ : binary_cofan (f 0) c‚ÇÅ.X}
  (t‚ÇÅ : is_colimit c‚ÇÅ) (t‚ÇÇ : is_colimit c‚ÇÇ) :
  is_colimit (extend_cofan c‚ÇÅ c‚ÇÇ) :=
{ desc := Œª s,
  begin
    apply (binary_cofan.is_colimit.desc' t‚ÇÇ (s.Œπ.app ‚ü®0‚ü©) _).1,
    apply t‚ÇÅ.desc ‚ü®_, discrete.nat_trans (Œª i, s.Œπ.app ‚ü®i.as.succ‚ü©)‚ü©
  end,
  fac' := Œª s,
  begin
    rintro ‚ü®j‚ü©,
    apply fin.induction_on j,
    { apply (binary_cofan.is_colimit.desc' t‚ÇÇ _ _).2.1 },
    { rintro i -,
      dsimp only [extend_cofan_Œπ_app],
      rw [fin.cases_succ, assoc, (binary_cofan.is_colimit.desc' t‚ÇÇ _ _).2.2, t‚ÇÅ.fac],
      refl }
  end,
  uniq' := Œª s m w,
  begin
    apply binary_cofan.is_colimit.hom_ext t‚ÇÇ,
    { rw (binary_cofan.is_colimit.desc' t‚ÇÇ _ _).2.1,
      apply w ‚ü®0‚ü© },
    { rw (binary_cofan.is_colimit.desc' t‚ÇÇ _ _).2.2,
      apply t‚ÇÅ.uniq ‚ü®_, _‚ü©,
      rintro ‚ü®j‚ü©,
      dsimp only [discrete.nat_trans_app],
      rw ‚Üê w ‚ü®j.succ‚ü©,
      dsimp only [extend_cofan_Œπ_app],
      rw [fin.cases_succ, assoc], }
  end }

section
variables [has_binary_coproducts C] [has_initial C]

/--
If `C` has an initial object and binary coproducts, then it has a coproduct for objects indexed by
`fin n`.
This is a helper lemma for `has_cofinite_products_of_has_binary_and_terminal`, which is more general
than this.
-/
private lemma has_coproduct_fin :
  Œ† (n : ‚Ñï) (f : fin n ‚Üí C), has_coproduct f
| 0 := Œª f,
  begin
    letI : has_colimits_of_shape (discrete (fin 0)) C :=
      has_colimits_of_shape_of_equivalence (discrete.equivalence.{0} fin_zero_equiv'.symm),
    apply_instance,
  end
| (n+1) := Œª f,
  begin
    haveI := has_coproduct_fin n,
    apply has_colimit.mk
      ‚ü®_, extend_cofan_is_colimit f (colimit.is_colimit _) (colimit.is_colimit _)‚ü©,
  end

/-- If `C` has an initial object and binary coproducts, then it has finite coproducts. -/
lemma has_finite_coproducts_of_has_binary_and_initial : has_finite_coproducts C :=
begin
  refine ‚ü®Œª n, ‚ü®Œª K, _‚ü©‚ü©,
  letI := has_coproduct_fin n (Œª n, K.obj ‚ü®n‚ü©),
  let : K ‚âÖ discrete.functor (Œª n, K.obj ‚ü®n‚ü©) := discrete.nat_iso (Œª ‚ü®i‚ü©, iso.refl _),
  apply has_colimit_of_iso this,
end

end

section preserves
variables (F : C ‚•§ D)
variables [preserves_colimits_of_shape (discrete walking_pair) F]
variables [preserves_colimits_of_shape (discrete.{0} pempty) F]
variables [has_finite_coproducts.{v} C]

/--
If `F` preserves the initial object and binary coproducts, then it preserves products indexed by
`fin n` for any `n`.
-/
noncomputable def preserves_fin_of_preserves_binary_and_initial  :
  Œ† (n : ‚Ñï) (f : fin n ‚Üí C), preserves_colimit (discrete.functor f) F
| 0 := Œª f,
  begin
    letI : preserves_colimits_of_shape (discrete (fin 0)) F :=
      preserves_colimits_of_shape_of_equiv.{0 0}
        (discrete.equivalence fin_zero_equiv'.symm) _,
    apply_instance,
  end
| (n+1) :=
  begin
    haveI := preserves_fin_of_preserves_binary_and_initial n,
    intro f,
    refine preserves_colimit_of_preserves_colimit_cocone
      (extend_cofan_is_colimit f (colimit.is_colimit _) (colimit.is_colimit _)) _,
    apply (is_colimit_map_cocone_cofan_mk_equiv _ _ _).symm _,
    let := extend_cofan_is_colimit (Œª i, F.obj (f i))
              (is_colimit_of_has_coproduct_of_preserves_colimit F _)
              (is_colimit_of_has_binary_coproduct_of_preserves_colimit F _ _),
    refine is_colimit.of_iso_colimit this _,
    apply cocones.ext _ _,
    apply iso.refl _,
    rintro ‚ü®j‚ü©,
    apply fin.induction_on j,
    { apply category.comp_id },
    { rintro i -,
      dsimp only [extend_cofan_Œπ_app, iso.refl_hom, cofan.mk_Œπ_app],
      rw [fin.cases_succ, fin.cases_succ],
      erw [comp_id, ‚ÜêF.map_comp],
      refl, }
  end

/--
If `F` preserves the initial object and binary coproducts, then it preserves colimits of shape
`discrete (fin n)`.
-/
def preserves_shape_fin_of_preserves_binary_and_initial (n : ‚Ñï) :
  preserves_colimits_of_shape (discrete (fin n)) F :=
{ preserves_colimit := Œª K,
  begin
    let : discrete.functor (Œª n, K.obj ‚ü®n‚ü©) ‚âÖ K := discrete.nat_iso (Œª ‚ü®i‚ü©, iso.refl _),
    haveI := preserves_fin_of_preserves_binary_and_initial F n (Œª n, K.obj ‚ü®n‚ü©),
    apply preserves_colimit_of_iso_diagram F this,
  end }

/-- If `F` preserves the initial object and binary coproducts then it preserves finite products. -/
def preserves_finite_coproducts_of_preserves_binary_and_initial
  (J : Type) [fintype J] :
  preserves_colimits_of_shape (discrete J) F :=
begin
  classical,
  let e := fintype.equiv_fin J,
  haveI := preserves_shape_fin_of_preserves_binary_and_initial F (fintype.card J),
  apply preserves_colimits_of_shape_of_equiv.{0 0} (discrete.equivalence e).symm,
end

end preserves

end category_theory
