/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import category_theory.category.basic
import category_theory.functor.basic
import category_theory.groupoid
import tactic.nth_rewrite
import category_theory.path_category
import category_theory.quotient
import combinatorics.quiver.symmetric

/-!
# Free groupoid on a quiver

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the free groupoid on a quiver, the lifting of a prefunctor to its unique
extension as a functor from the free groupoid, and proves uniqueness of this extension.

## Main results

Given the type `V` and a quiver instance on `V`:

- `free_groupoid V`: a type synonym for `V`.
- `free_groupoid_groupoid`: the `groupoid` instance on `free_groupoid V`.
- `lift`: the lifting of a prefunctor from `V` to `V'` where `V'` is a groupoid, to a functor.
 `free_groupoid V â¥¤ V'`.
- `lift_spec` and `lift_unique`: the proofs that, respectively, `lift` indeed is a lifting
 and is the unique one.

## Implementation notes

The free groupoid is first defined by symmetrifying the quiver, taking the induced path category
and finally quotienting by the reducibility relation.

-/

open set classical function
local attribute [instance] prop_decidable

namespace category_theory
namespace groupoid
namespace free

universes u v u' v' u'' v''

variables {V : Type u} [quiver.{v+1} V]

/-- Shorthand for the "forward" arrow corresponding to `f` in `paths $ symmetrify V` -/
abbreviation quiver.hom.to_pos_path {X Y : V} (f : X âŸ¶ Y) :
 ((category_theory.paths.category_paths $ quiver.symmetrify V).hom X Y) := f.to_pos.to_path

/-- Shorthand for the "forward" arrow corresponding to `f` in `paths $ symmetrify V` -/
abbreviation quiver.hom.to_neg_path {X Y : V} (f : X âŸ¶ Y) :
 ((category_theory.paths.category_paths $ quiver.symmetrify V).hom Y X) := f.to_neg.to_path

/-- The "reduction" relation -/
inductive red_step : hom_rel (paths (quiver.symmetrify V))
| step (X Z : quiver.symmetrify V) (f : X âŸ¶ Z) :
 red_step (ğŸ™ X) (f.to_path â‰« (quiver.reverse f).to_path)

/-- The underlying vertices of the free groupoid -/
def _root_.category_theory.free_groupoid (V) [Q : quiver V] := quotient (@red_step V Q)

instance {V} [Q : quiver V] [h : nonempty V] : nonempty (free_groupoid V) := âŸ¨âŸ¨h.someâŸ©âŸ©

lemma congr_reverse {X Y : paths $ quiver.symmetrify V} (p q : X âŸ¶ Y) :
 quotient.comp_closure red_step p q â†’
 quotient.comp_closure red_step (p.reverse) (q.reverse) :=
begin
 rintro âŸ¨XW, pp, qq, WY, _, Z, fâŸ©,
 have : quotient.comp_closure red_step (WY.reverse â‰« ğŸ™ _ â‰« XW.reverse)
 (WY.reverse â‰« (f.to_path â‰« (quiver.reverse f).to_path) â‰« XW.reverse),
 { apply quotient.comp_closure.intro,
 apply red_step.step, },
 simpa only [category_struct.comp, category_struct.id, quiver.path.reverse, quiver.path.nil_comp,
 quiver.path.reverse_comp, quiver.reverse_reverse, quiver.path.reverse_to_path,
 quiver.path.comp_assoc] using this,
end

lemma congr_comp_reverse {X Y : paths $ quiver.symmetrify V} (p : X âŸ¶ Y) :
 quot.mk (@quotient.comp_closure _ _ red_step _ _) (p â‰« p.reverse) =
 quot.mk (@quotient.comp_closure _ _ red_step _ _) (ğŸ™ X) :=
begin
 apply quot.eqv_gen_sound,
 induction p with _ _ q f ih,
 { apply eqv_gen.refl, },
 { simp only [quiver.path.reverse],
 fapply eqv_gen.trans,
 { exact q â‰« q.reverse, },
 { apply eqv_gen.symm, apply eqv_gen.rel,
 have : quotient.comp_closure
 red_step (q â‰« (ğŸ™ _) â‰« q.reverse)
 (q â‰« (f.to_path â‰« (quiver.reverse f).to_path) â‰« q.reverse), by
 { apply quotient.comp_closure.intro, apply red_step.step, },
 have that : q.cons f = q.comp f.to_path, by refl, rw that,
 simp only [category.assoc, category.id_comp] at this âŠ¢,
 simp only [category_struct.comp, quiver.path.comp_assoc] at this âŠ¢,
 exact this, },
 { exact ih }, },
end

lemma congr_reverse_comp {X Y : paths $ quiver.symmetrify V} (p : X âŸ¶ Y) :
 quot.mk (@quotient.comp_closure _ _ red_step _ _) (p.reverse â‰« p) =
 quot.mk (@quotient.comp_closure _ _ red_step _ _) (ğŸ™ Y) :=
begin
 nth_rewrite 1 â†quiver.path.reverse_reverse p,
 apply congr_comp_reverse,
end

instance : category (free_groupoid V) := quotient.category red_step

/-- The inverse of an arrow in the free groupoid -/
def quot_inv {X Y : free_groupoid V} (f : X âŸ¶ Y) : Y âŸ¶ X :=
quot.lift_on f
 (Î» pp, quot.mk _ $ pp.reverse)
 (Î» pp qq con, quot.sound $ congr_reverse pp qq con)

instance : groupoid (free_groupoid V) :=
{ inv := Î» X Y f, quot_inv f,
 inv_comp' := Î» X Y p, quot.induction_on p $ Î» pp, congr_reverse_comp pp,
 comp_inv' := Î» X Y p, quot.induction_on p $ Î» pp, congr_comp_reverse pp }

/-- The inclusion of the quiver on `V` to the underlying quiver on `free_groupoid V`-/
def of (V) [quiver V] : V â¥¤q (free_groupoid V) :=
{ obj := Î» X, âŸ¨XâŸ©,
 map := Î» X Y f, quot.mk _ f.to_pos_path }

lemma of_eq : of V =
 (quiver.symmetrify.of â‹™q paths.of).comp (quotient.functor $ @red_step V _).to_prefunctor :=
begin
 apply prefunctor.ext, rotate,
 { rintro X, refl, },
 { rintro X Y f, refl, }
end

section universal_property

variables {V' : Type u'} [groupoid V'] (Ï† : V â¥¤q V')

/-- The lift of a prefunctor to a groupoid, to a functor from `free_groupoid V` -/
def lift (Ï† : V â¥¤q V') : free_groupoid V â¥¤ V' :=
quotient.lift _
 (paths.lift $ quiver.symmetrify.lift Ï†)
 (by
 { rintros _ _ _ _ âŸ¨X,Y,fâŸ©,
 simp only [quiver.symmetrify.lift_reverse, paths.lift_nil, quiver.path.comp_nil,
 paths.lift_cons, paths.lift_to_path],
 symmetry,
 apply groupoid.comp_inv, })

lemma lift_spec (Ï† : V â¥¤q V') : of V â‹™q (lift Ï†).to_prefunctor = Ï† :=
begin
 rw [of_eq]; rw [ prefunctor.comp_assoc]; rw [ prefunctor.comp_assoc]; rw [ functor.to_prefunctor_comp],
 dsimp [lift],
 rw [quotient.lift_spec]; rw [ paths.lift_spec]; rw [ quiver.symmetrify.lift_spec],
end

lemma lift_unique (Ï† : V â¥¤q V') (Î¦ : free_groupoid V â¥¤ V')
 (hÎ¦ : of V â‹™q Î¦.to_prefunctor = Ï†) : Î¦ = lift Ï† :=
begin
 apply quotient.lift_unique,
 apply paths.lift_unique,
 fapply @quiver.symmetrify.lift_unique _ _ _ _ _ _ _ _ _,
 { rw â†functor.to_prefunctor_comp, exact hÎ¦, },
 { constructor, rintros X Y f,
 simp only [â†functor.to_prefunctor_comp,prefunctor.comp_map, paths.of_map, inv_eq_inv],
 change Î¦.map (inv ((quotient.functor red_step).to_prefunctor.map f.to_path)) =
 inv (Î¦.map ((quotient.functor red_step).to_prefunctor.map f.to_path)),
 have := functor.map_inv Î¦ ((quotient.functor red_step).to_prefunctor.map f.to_path),
 convert this; simp only [inv_eq_inv], },
end

end universal_property

section functoriality

variables {V' : Type u'} [quiver.{v'+1} V'] {V'' : Type u''} [quiver.{v''+1} V'']

/-- The functor of free groupoid induced by a prefunctor of quivers -/
def _root_.category_theory.free_groupoid_functor (Ï† : V â¥¤q V') :
 free_groupoid V â¥¤ free_groupoid V' := lift (Ï† â‹™q of V')

lemma free_groupoid_functor_id :
 free_groupoid_functor (prefunctor.id V) = functor.id (free_groupoid V) :=
begin
 dsimp only [free_groupoid_functor], symmetry,
 apply lift_unique, refl,
end

lemma free_groupoid_functor_comp
 (Ï† : V â¥¤q V') (Ï†' : V' â¥¤q V'') :
 free_groupoid_functor (Ï† â‹™q Ï†') = free_groupoid_functor Ï† â‹™ free_groupoid_functor Ï†' :=
begin
 dsimp only [free_groupoid_functor], symmetry,
 apply lift_unique, refl,
end

end functoriality

end free
end groupoid
end category_theory

